
#Область СлужебныйПрограммныйИнтерфейс

// В этом методе мы добавляем свои пункты меню расширенного глобального поиска
// 
// Параметры:
//  ПараметрыПоиска - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
//  ДополнительныеПараметры - Неопределено - Не используется, необходим для описания оповещения
//
Процедура ПослеЗаполненияПараметровГлобальногоПоиска(
	ПараметрыПоиска, ДополнительныеПараметры = Неопределено) Экспорт

	Перем РГП_ГлобальныйПоискКлиент, РГП_ГлобальныйПоискКлиентСервер;
	РГП_ГлобальныйПоискКлиент = РГП_ГлобальныйПоискКлиент();
	РГП_ГлобальныйПоискКлиентСервер = РГП_ГлобальныйПоискКлиентСервер();
	
	КорневоеМеню = РГП_ГлобальныйПоискКлиент.ОсновноеМенюРасширенногоГлобальногоПоиска(ПараметрыПоиска);
	
	НоваяКоманда = РГП_ГлобальныйПоискКлиент.НоваяКомандаРасширенногоПоиска(ПараметрыПоиска, "Toolkit", КорневоеМеню, 1);
	НоваяКоманда.Картинка = БиблиотекаКартинок.РГП_Toolkit;
	НоваяКоманда.Использование = РГП_ГлобальныйПоискКлиентСервер.ЭтоПолноправныйПользователь(ПараметрыПоиска);
	НоваяКоманда.Представление = "Infostart Toolkit";
	
	ЭлементПланаПоиска = РГП_ГлобальныйПоискКлиент.ЭлементПланаГлобальногоПоискаКоманды(
		"ГлобальныйПоискПоПунктамМеню", ИмяТекущегоМодуля(), Ложь, Ложь);
	
	НоваяКоманда.ПланПоиска.Добавить(ЭлементПланаПоиска);

КонецПроцедуры

// Метод плана глобального поиска по пунктам меню Infostart Toolkit
// 
// Параметры:
//  СтрокаПоиска - Строка - Строка, по которой выполняется поиск.
//  РезультатыПоиска - РезультатГлобальногоПоиска - Выходной параметр. Содержит результаты поиска.
//  ПараметрыВыполнения - см. РГП_ГлобальныйПоискКлиентСервер.ШаблонПараметровВыполненияКомандыПоиска
Процедура ГлобальныйПоискПоПунктамМеню(Знач СтрокаПоиска, РезультатыПоиска, ПараметрыВыполнения) Экспорт

	Перем РГП_ГлобальныйПоискКлиентСервер;
	РГП_ГлобальныйПоискКлиентСервер = РГП_ГлобальныйПоискКлиентСервер();

	ИмяТекущегоМодуля = ИмяТекущегоМодуля();

	ЕстьСтрокаПоиска = ЗначениеЗаполнено(ПараметрыВыполнения.СтрокаПоиска);

	ИмяФормыГлобальногоМенюИТК = "Обработка.ИТК_ГлобальноеМеню.Форма";
	ФормаГлобальногоМенюИТК = ПолучитьФорму(ИмяФормыГлобальногоМенюИТК);
	ДеревоОпераций = ФормаГлобальногоМенюИТК.ДоступныеОперации; // ДанныеФормыДерево
	
	Для Каждого ТекущаяОперация Из ДеревоОпераций.ПолучитьЭлементы() Цикл
		
		Представление = ТекущаяОперация.Представление;
		
		Если ЕстьСтрокаПоиска Тогда
			Представление = СтрНайтиИВыделитьОформлением(Представление, ПараметрыВыполнения.СловаПоиска);
			Если ПустаяСтрока(Представление) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ОписаниеОповещения = РГП_ГлобальныйПоискКлиентСервер.ОписаниеОповещения(
			"ОткрытьИнструмент", ИмяТекущегоМодуля, ТекущаяОперация);
		ДействиеОткрытия = РГП_ГлобальныйПоискКлиентСервер.ВыполняемоеДействиеОписанияОповещения(ОписаниеОповещения);
		
		РГП_ГлобальныйПоискКлиентСервер.ДобавитьЭлементРезультатаГлобальногоПоиска(
			РезультатыПоиска, ДействиеОткрытия, Представление, ТекущаяОперация.Картинка);
		
	КонецЦикла;

КонецПроцедуры

Процедура ОткрытьИнструмент(ВыполняемоеДействие, ТекущаяОперация) Экспорт

	ИТК_ТуллкитКлиент().ОткрытьИнструмент(ТекущаяОперация.Идентификатор, ТекущаяОперация.Объект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяТекущегоМодуля()
	
	Возврат "РГП_ГлобальныйПоискКлиентПереопределяемый_InfostartToolkit";
	
КонецФункции

Функция РГП_ГлобальныйПоискКлиент()
	
	Возврат ОбщийМодуль("РГП_ГлобальныйПоискКлиент");
	
КонецФункции

Функция РГП_ГлобальныйПоискКлиентСервер()
	
	Возврат ОбщийМодуль("РГП_ГлобальныйПоискКлиентСервер");
	
КонецФункции

Функция ИТК_ТуллкитКлиент()
	
	Возврат ОбщийМодуль("ИТК_ТуллкитКлиент");
	
КонецФункции

Функция ОбщийМодуль(ИмяМодуля)
	
	Возврат Вычислить(ИмяМодуля);
	
КонецФункции

#КонецОбласти
