
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолноеИмяМетаданных = Параметры.ПолноеИмяМетаданных;
	ОтобразитьСписок(ПолноеИмяМетаданных);
	Заголовок = СтрШаблон("Анализ данных: %1", ПолноеИмяМетаданных);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Отобразить регистр.
// 
// Параметры:
//  ПолноеИмяМетаданных - Строка
&НаСервере
Процедура ОтобразитьСписок(ПолноеИмяМетаданных)
	
	ЭлементСписок = Элементы.Список;
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданных);
	
	Список.ПроизвольныйЗапрос = Ложь;
	Список.ОсновнаяТаблица = ОбъектМетаданных.ПолноеИмя();
	
	ДоступныеПоля = Список.КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора.Элементы;
	Для Каждого Поле Из ДоступныеПоля Цикл
		//@skip-check empty-except-statement - нет смысла обрабатывать
		Попытка
			ПутьКДанным = СтрШаблон("%1.%2", ЭлементСписок.Имя, Поле.Поле);
			ИмяЭлемента = СтрШаблон("%1_%2", ЭлементСписок.Имя, Поле.Поле);
			НовыйЭлемент = Элементы.Добавить(
				ИмяЭлемента, Тип("ПолеФормы"), Элементы.Список);
			НовыйЭлемент.ПутьКДанным = ПутьКДанным;
			НовыйЭлемент.Высота = 1;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
