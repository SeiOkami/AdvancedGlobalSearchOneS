// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// При начале работы системы.
Процедура ПриНачалеРаботыСистемы() Экспорт
	
	УстановитьОписаниеГлобальногоПоиска();
	УстановитьПодсказкуВводаГлобальногоПоиска();
	
КонецПроцедуры

// Событие при глобальном поиске.
// 
// Параметры:
//  СтрокаПоиска - Строка
//  ПланПоиска - ПланГлобальногоПоиска
Процедура ПриГлобальномПоиске(СтрокаПоиска, ПланПоиска) Экспорт
	
	КлючПоискаМетаданных = РГП_ГлобальныйПоискКлиентСервер.КлючГлобальногоПоискаМетаданных();
	Если СтрНачинаетсяС(СтрокаПоиска, КлючПоискаМетаданных) Тогда
		ПланПоиска.Очистить();
		ПланПоиска.Добавить("ГлобальныйПоискМетаданные", "РГП_ГлобальныйПоискСервер", Истина, Ложь);
	КонецЕсли;
	
	РГП_ГлобальныйПоискКлиентПереопределяемый.Модуль().ПриГлобальномПоиске(СтрокаПоиска, ПланПоиска);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Установить описание глобального поиска.
Процедура УстановитьОписаниеГлобальногоПоиска()
	
	ЖирныйШрифт = Новый Шрифт(,,Истина);
	
	Описание = Новый Массив;
	
	Описание.Добавить(НСтр("ru = 'Введите'") + " ");
	Описание.Добавить(Новый ФорматированнаяСтрока(
		РГП_ГлобальныйПоискКлиентСервер.КлючГлобальногоПоискаМетаданных(), ЖирныйШрифт));
	Описание.Добавить(НСтр("ru = ' для поиска метаданных'"));
	
	ГлобальныйПоиск.УстановитьОписание(Новый ФорматированнаяСтрока(Описание));
	
КонецПроцедуры

// Установить описание глобального поиска.
Процедура УстановитьПодсказкуВводаГлобальногоПоиска()
	
	ГлобальныйПоиск.УстановитьПодсказкуВвода(Строка(ГлобальныйПоиск.ПолучитьОписание()));
	
КонецПроцедуры

#КонецОбласти
