// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Значение соответствует шаблону.
// 
// Параметры:
//  Значение - Произвольный
//           - Структура из КлючИЗначение:
//           * Ключ - Строка
//  Шаблон - Структура - Шаблон структуры, наличия свойств которых нужно проверить в значении
// 
// Возвращаемое значение:
//  Булево
Функция ЗначениеСоответствуетШаблону(Значение, Шаблон) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Структура") Тогда
		
		Соответствует = Истина;
		
		Если Шаблон.Количество() <> Значение.Количество() Тогда
			Соответствует = Ложь;
		Иначе
			Для Каждого КлючИЗначение Из Шаблон Цикл
				Если НЕ Значение.Свойство(КлючИЗначение.Ключ) Тогда
					Соответствует = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
	Иначе
		
		Соответствует = Ложь;
		
	КонецЕсли;
	
	Возврат Соответствует;
	
КонецФункции

#Область ВыполняемыеДействия

// Пустое описание оповещения.
// 
// Возвращаемое значение:
//  см. ОписаниеОповещения
Функция ПустоеОписаниеОповещения() Экспорт
	
	Возврат ОписаниеОповещения("", "");
	
КонецФункции

// Описание оповещения
//  
// Параметры: 
//  ИмяПроцедуры - Строка, Неопределено - Имя экспортируемой процедуры модуля, которая будет вызвана.
//  Модуль - ОбщийМодуль, Строка - Программный модуль, процедура которого будет вызвана
//  ДополнительныеПараметры - Неопределено, Произвольный - Значение любого типа, 
//		которое при вызове будет передано в указанную процедуру последним параметром.
//  ИмяПроцедурыОбработкиОшибки - Строка - Имя экспортируемой процедуры модуля, 
//		которая будет вызвана в случае возникновения ошибки.
//  МодульОбработкиОшибки - ОбщийМодуль, Строка, Неопределено - Программный модуль, 
//		процедура которого будет вызвана в случае возникновения ошибки
//		При использовании типа ФормаКлиентскогоПриложения 
//		будет вызван метод модуля указанной формы клиентского приложения. 
//		При использовании типа КомандаКомандногоИнтерфейса 
//		будет вызван метод модуля команды командного интерфейса. 
//		При использовании типа ОбщийМодуль будет вызван метод общего неглобального модуля.
// 
// Возвращаемое значение:
//  Структура - Описание оповещения:
// * ИмяПроцедуры - см. ОписаниеОповещения.ИмяПроцедуры
// * Модуль - см. ОписаниеОповещения.Модуль
// * ДополнительныеПараметры - см. ОписаниеОповещения.ДополнительныеПараметры
// * ИмяПроцедурыОбработкиОшибки - см. ОписаниеОповещения.ИмяПроцедурыОбработкиОшибки
// * МодульОбработкиОшибки - ОбщийМодуль, Строка, Неопределено -
//
// Отключаемые проверки:
//	BSLLS:MissingReturnedValueDescription-off - баг BSL LS
//	BSLLS:MissingParameterDescription-off - баг BSL LS
Функция ОписаниеОповещения(ИмяПроцедуры, Модуль, ДополнительныеПараметры = Неопределено, 
	ИмяПроцедурыОбработкиОшибки = "", МодульОбработкиОшибки = "") Экспорт
	
	ОписаниеОповещения = Новый Структура;
	ОписаниеОповещения.Вставить("ИмяПроцедуры", ИмяПроцедуры);
	ОписаниеОповещения.Вставить("Модуль", Модуль);
	ОписаниеОповещения.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	ОписаниеОповещения.Вставить("ИмяПроцедурыОбработкиОшибки", ИмяПроцедурыОбработкиОшибки);
	ОписаниеОповещения.Вставить("МодульОбработкиОшибки", МодульОбработкиОшибки);
	
	// @skip-check constructor-function-return-section - баг ЕДТ
	Возврат ОписаниеОповещения;
	
КонецФункции

// Параметры для метода см. ВыполняемоеДействиеВводаЗначения
// 
// Параметры:
//  ОписаниеОповещенияОЗавершении - см. ОписаниеОповещения
//  Значение - Произвольный
//  Подсказка - Строка
//  Тип - ОписаниеТипов
// 
// Возвращаемое значение:
//  Структура - :
// * ОписаниеОповещенияОЗавершении - см. ПараметрыВыполняемогоДействияВводаЗначения.ОписаниеОповещенияОЗавершении
// * Значение - см. ПараметрыВыполняемогоДействияВводаЗначения.Значение
// * Подсказка - см. ПараметрыВыполняемогоДействияВводаЗначения.Подсказка
// * Тип - см. ПараметрыВыполняемогоДействияВводаЗначения.Тип
Функция ПараметрыВыполняемогоДействияВводаЗначения(ОписаниеОповещенияОЗавершении, Значение, Подсказка, Тип) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОписаниеОповещенияОЗавершении", ОписаниеОповещенияОЗавершении);
	Результат.Вставить("Значение", Значение);
	Результат.Вставить("Подсказка", Подсказка);
	Результат.Вставить("Тип", Тип);
	
	//@skip-check constructor-function-return-section - Баг ЕДТ
	Возврат Результат;
	
КонецФункции

// Выполняемое действие ввода значения.
// 
// Параметры:
//  ПараметрыДействия - см. ПараметрыВыполняемогоДействияВводаЗначения
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействиеОписанияОповещения
Функция ВыполняемоеДействиеВводаЗначения(ПараметрыДействия) Экспорт
	
	ОписаниеОповещения = ОписаниеОповещения("ОтобразитьВводЗначения", "РГП_ГлобальныйПоискКлиент", ПараметрыДействия);
	Возврат ВыполняемоеДействиеОписанияОповещения(ОписаниеОповещения);
	
КонецФункции

// Выполняемое действие набора выполняемых действий.
// 
// Параметры:
//  Действие1 - см. ВыполняемоеДействие
//  Действие2 - см. ВыполняемоеДействие
//  Действие3 - см. ВыполняемоеДействие
//  
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеНабораДействий(Знач Действие1 = Неопределено, 
	Знач Действие2 = Неопределено, Знач Действие3 = Неопределено) Экспорт
	
	Результат = ВыполняемоеДействие();
	Если Действие1 <> Неопределено Тогда
		Результат.ВыполняемыеДействия.Добавить(Действие1);
	КонецЕсли;
	Если Действие2 <> Неопределено Тогда
		Результат.ВыполняемыеДействия.Добавить(Действие2);
	КонецЕсли;
	Если Действие3 <> Неопределено Тогда
		Результат.ВыполняемыеДействия.Добавить(Действие3);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Выполняемое действие. Может использоваться в команде и в результате поиска
// 
// Возвращаемое значение:
//  Структура:
// * ОткрываемоеЗначение - Неопределено, Произвольный - Значение, которое необходимо открыть
// * КомандаПоиска - Строка - Полный путь команды поиска
// * ПараметрыПоиска - Структура - Параметры команды поиска
// * СтрокаПоиска - Строка - Строка команды поиска
// * ОткрываемаяФорма - см. ОписаниеОткрываемойФормы
// * ОписаниеОповещения - см. ОписаниеОповещения
// * ВыполняемыеДействия - Массив из см. ВыполняемоеДействие
// * ДополнительныеПараметры - Структура из КлючИЗначение - :
// ** Ключ - Строка
// ** Значение - Произвольный
Функция ВыполняемоеДействие() Экспорт

	ВыполняемыеДействия = Новый Массив; // Массив из см. ВыполняемоеДействие
	
	Результат = Новый Структура;
	
	// Выполняемое действие - Команда поиска
	Результат.Вставить("КомандаПоиска", "");
	Результат.Вставить("ПараметрыПоиска", Новый Структура);
	Результат.Вставить("СтрокаПоиска", "");
	
	// Выполняемое действие - Открытие значения
	Результат.Вставить("ОткрываемоеЗначение", Неопределено);	
	
	// Выполняемое действие - Вызов метода
	Результат.Вставить("ОписаниеОповещения", Неопределено);
	
	// Выполняемое действие - Открытие формы
	Результат.Вставить("ОткрываемаяФорма", Неопределено);
	
	// Выполняемое действие - Набор действий
	Результат.Вставить("ВыполняемыеДействия", ВыполняемыеДействия);
	
	// Доп. параметры
	Результат.Вставить("ДополнительныеПараметры", Новый Структура);
	
	// @skip-check constructor-function-return-section - #EDT786
	Возврат Результат;
	
КонецФункции

// Описание открываемой формы.
// 
// Параметры:
//  ИмяФормы - Строка - Имя открываемой формы
//  Параметры - Структура, Неопределено - Параметры формы
// 
// Возвращаемое значение:
//  Структура - Описание открываемой формы:
// * ИмяФормы - см. ОписаниеОткрываемойФормы.ИмяФормы
// * Параметры - см. ОписаниеОткрываемойФормы.Параметры
// * Владелец - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.Владелец
// * Уникальность - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.Уникальность
// * Окно - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.Окно
// * НавигационнаяСсылка - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.НавигационнаяСсылка
// * ОписаниеОповещенияОЗакрытии - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.ОписаниеОповещенияОЗакрытии
// * РежимОткрытияОкна - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.РежимОткрытияОкна
Функция ОписаниеОткрываемойФормы(ИмяФормы, Параметры = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяФормы", ИмяФормы);
	Результат.Вставить("Параметры", Параметры);
	Результат.Вставить("Владелец", Неопределено);
	Результат.Вставить("Уникальность", Неопределено);
	Результат.Вставить("Окно", Неопределено);
	Результат.Вставить("НавигационнаяСсылка", Неопределено);
	Результат.Вставить("ОписаниеОповещенияОЗакрытии", Неопределено);
	Результат.Вставить("РежимОткрытияОкна", Неопределено);

	Возврат Результат;
	
КонецФункции

// Выполняемое действие открытия формы.
// 
// Параметры:
//  ИмяФормы - Строка - Имя открываемой формы
//  ПараметрыФормы - Структура, Неопределено - Параметры формы
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеОткрытияФормы(ИмяФормы, ПараметрыФормы = Неопределено) Экспорт
	
	Результат = ВыполняемоеДействие();
	Результат.ОткрываемаяФорма = ОписаниеОткрываемойФормы(ИмяФормы, ПараметрыФормы);
	Возврат Результат;
	
КонецФункции

// см. ВыполняемоеДействиеОткрытияЗначения
Функция ВыполняемоеДействиеПустое() Экспорт
	
	Возврат Неопределено;
	
КонецФункции

// Выполняемое действие открытия значения.
// 
// Параметры:
//  ОткрываемоеЗначение - Произвольный
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеОткрытияЗначения(ОткрываемоеЗначение) Экспорт
	
	Результат = ВыполняемоеДействие();
	Результат.ОткрываемоеЗначение = ОткрываемоеЗначение;
	
	Возврат Результат;
	
КонецФункции

// Выполняемое действие выполнения описания оповещения
// 
// Параметры:
//  ОписаниеОповещения - см. ОписаниеОповещения
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеОписанияОповещения(ОписаниеОповещения) Экспорт
	
	Результат = ВыполняемоеДействие();
	Результат.ОписаниеОповещения = ОписаниеОповещения;
	
	Возврат Результат;
	
КонецФункции

// Выполняемое действие перехода назад (выход из параметров)
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
//  ВыходИзПараметров - см. Массив
//  ИсключаяПараметры - см. Массив
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеНазадИзПараметров(ПараметрыВыполнения, 
	ВыходИзПараметров = Неопределено, ИсключаяПараметры = Неопределено) Экспорт
	
	ПараметрыКоманды = Структура(ПараметрыВыполнения.ПараметрыКоманды, ВыходИзПараметров, ИсключаяПараметры, Истина);
	Возврат ВыполняемоеДействиеПереходаНаКоманду(
		ПараметрыВыполнения.ПолныйКлюч, ПараметрыКоманды, ПараметрыВыполнения.СтрокаПоиска);
	
КонецФункции

// Выполняемое действие обновления результатов поиска
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеОбновитьРезультаты(ПараметрыВыполнения) Экспорт
	
	Возврат ВыполняемоеДействиеПереходаНаКоманду(ПараметрыВыполнения.ПолныйКлюч, 
		ПараметрыВыполнения.ПараметрыКоманды, ПараметрыВыполнения.СтрокаПоиска);
	
КонецФункции

// Выполняемое действие перехода назад (на родительскую команду).
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействиеПереходаНаКоманду
Функция ВыполняемоеДействиеПереходаНазад(ПараметрыВыполнения) Экспорт
	
	Если ПараметрыВыполнения.ПриПереходеНазадОчищатьСтрокуПоиска Тогда
		СтрокаПоиска = "";
	Иначе
		СтрокаПоиска = ПараметрыВыполнения.СтрокаПоиска;
	КонецЕсли;
	
	ВыходИзПараметров = Ложь;
	Если НЕ ПараметрыВыполнения.ПриПереходеНазадОчищатьПараметры Тогда
		Для Каждого КлючИЗначение Из ПараметрыВыполнения.ПараметрыКоманды Цикл
			Если НЕ КлючИЗначение.Ключ = КлючПараметраКоманды_Страница() Тогда
				ВыходИзПараметров = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ВыходИзПараметров Тогда
		ВыполняемаяКоманда = ПараметрыВыполнения.ПолныйКлюч;
	Иначе
		ВыполняемаяКоманда = ПараметрыВыполнения.ПолныйКлючРодителя;
	КонецЕсли;
	
	Возврат ВыполняемоеДействиеПереходаНаКоманду(ВыполняемаяКоманда, , СтрокаПоиска);
	
КонецФункции

// Выполняемое действие перехода на подчиненную команду поиска (с передачей текущих параметров и строки поиска)
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
//  КлючКоманды - Строка - Ключ подчиненной команды
//  ПередаваемыеПараметры - Структура - Дополнительные передаваемые параметры
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеПереходаНаПодчиненнуюКоманду(ПараметрыВыполнения, 
	КлючКоманды = "", ПередаваемыеПараметры = Неопределено) Экспорт
	
	ПолныйКлюч = ПараметрыВыполнения.ПолныйКлюч;
	
	ЧастиПути = Новый Массив; // Массив из Строка
	ЧастиПути.Добавить(ПолныйКлюч);
	Если ЗначениеЗаполнено(КлючКоманды) Тогда
		ЧастиПути.Добавить(КлючКоманды);
		ЧастиПути.Добавить(ПараметрыВыполнения.Разделитель);
	КонецЕсли;
	
	ПолныйПуть = СтрСоединить(ЧастиПути, "");
	
	Возврат ВыполняемоеДействиеПереходаНаКоманду(ПолныйПуть, 
		ПередаваемыеПараметры, ПараметрыВыполнения.СтрокаПоиска);
	
КонецФункции

// Выполняемое действие перехода на команду поиска
// 
// Параметры:
//  ПолныйКлючКоманды - Строка - Полный ключ выполняемой команды поиска
//  ПередаваемыеПараметры - Неопределено, Структура - Параметры выполняения команды
//  СтрокаПоиска - Строка - Строка поиска в команде
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеПереходаНаКоманду(ПолныйКлючКоманды = "", 
	ПередаваемыеПараметры = Неопределено, СтрокаПоиска = "") Экспорт
	
	Результат = ВыполняемоеДействие();
	Результат.КомандаПоиска = ПолныйКлючКоманды;
	Результат.СтрокаПоиска = СтрокаПоиска;
	
	Если ТипЗнч(ПередаваемыеПараметры) = Тип("Структура") Тогда
		Результат.ПараметрыПоиска = ПередаваемыеПараметры;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Выполняемое действие ввода номера страницы.
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
//  ТекущаяСтраница - Число
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействиеВводаЗначения
Функция ВыполняемоеДействиеВводНомераСтраницы(ПараметрыВыполнения, ТекущаяСтраница) Экспорт
	
	ВыполняемоеДействие = ВыполняемоеДействиеОбновитьРезультаты(ПараметрыВыполнения);
	ОписаниеОповещения = ОписаниеОповещения("ПерейтиНаСтраницу", "РГП_ГлобальныйПоискКлиент", ВыполняемоеДействие);
	
	ПараметрыВвода = ПараметрыВыполняемогоДействияВводаЗначения(ОписаниеОповещения, 
		ТекущаяСтраница, "Введите страницу", ОписаниеТипаПараметраКоманды_Страница());
	
	Возврат ВыполняемоеДействиеВводаЗначения(ПараметрыВвода);
	
КонецФункции

// Выполняемое действие перехода на страницу.
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
//  Страница - Число
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействиеПереходаНаКоманду
Функция ВыполняемоеДействиеПереходаНаСтраницу(ПараметрыВыполнения, Страница) Экспорт
	
	СтрокаПоиска = ПараметрыВыполнения.СтрокаПоиска;
	ПараметрыКоманды = Новый Структура(Новый ФиксированнаяСтруктура(ПараметрыВыполнения.ПараметрыКоманды));
	ПараметрыКоманды.Вставить(КлючПараметраКоманды_Страница(), Страница);
	
	Возврат ВыполняемоеДействиеПереходаНаКоманду(ПараметрыВыполнения.ПолныйКлюч, ПараметрыКоманды, СтрокаПоиска);
	
КонецФункции

#КонецОбласти

// Пустые параметры выполнения команды
// Свойства наследуются из см. РГП_ГлобальныйПоискКлиент.НоваяКомандаРасширенногоПоиска
// Заполняется в клиенстком модуле и передается в план поиска
// 
// Возвращаемое значение:
//  Структура - :
// * Ключ - Строка - Ключ команды
// * ПолныйКлюч - Строка - Полный ключ команды
// * ПолныйКлючРодителя - Строка - Полный ключ перехода к команде родителя
// * Разделитель - Строка - Разделитель команд
// * Представление - Строка - Строка, выводимая пользователю
// * ПараметрыКоманды - Структура - Значения параметров команды
// * ПолныйКлючСПараметрами - Строка - Полный ключ команды, включая параметры
// * ПолнаяСтрокаПоиска - Строка - Полная строка поиска (содержимое поля)
// * СтрокаПоиска - Строка - Строка поиска с вырезанным ключем команды
// * СтрокаПоискаВрег - Строка - СтрокаПоиска в верхнем регистре
// * СловаПоиска - Массив из Строка - СтрокаПоискаВрег разбитая на слова
// * МаксимальныйПриоритет - Число - Максимальный приоритет, устанавливаемый для идеального совпадения по строке поиска
// * СтандартныеКоманды - Массив из Строка - Массив стандартных вспомогательных команд в начале списка результата, см. СтандартныеКоманды
// * ПользовательскиеНастройки - см. НовыеОбщиеПользовательскиеНастройки
// * ПриПереходеНазадОчищатьСтрокуПоиска - Булево - Стоит ли при переходе "назад" очищать строку поиска
// * ПриПереходеНазадОчищатьПараметры - Булево - Стоит ли при переходе "назад" очищать параметры
// * ДополнительныеПараметры - Структура из КлючИЗначение:
// ** Ключ - Строка
// ** Значение - Произвольный
Функция ШаблонПараметровВыполненияКомандыПоиска() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ключ", "");
	Результат.Вставить("ПолныйКлюч", "");
	Результат.Вставить("ПолныйКлючРодителя", "");
	Результат.Вставить("Разделитель", "");
	Результат.Вставить("Представление", "");
	Результат.Вставить("ПараметрыКоманды", Новый Структура);
	Результат.Вставить("ПолныйКлючСПараметрами", "");
	Результат.Вставить("ПолнаяСтрокаПоиска", "");
	Результат.Вставить("СтрокаПоиска", "");
	Результат.Вставить("СтрокаПоискаВрег", "");
	Результат.Вставить("СловаПоиска", Новый Массив);
	Результат.Вставить("МаксимальныйПриоритет", 999);
	Результат.Вставить("СтандартныеКоманды", Новый Массив);
	Результат.Вставить("ПользовательскиеНастройки", НовыеОбщиеПользовательскиеНастройки());
	Результат.Вставить("ПриПереходеНазадОчищатьСтрокуПоиска", Ложь);
	Результат.Вставить("ПриПереходеНазадОчищатьПараметры", Ложь);
	Результат.Вставить("ДополнительныеПараметры", Новый Структура);
	
	Возврат Результат;
	
КонецФункции

// Ключ вида расширенного глобального поиска. 
// По нему определяем, что при нажатии нужно выполнять свою логику
// 
// Возвращаемое значение:
//  Строка
Функция КлючВидаРасширенногоГлобальногоПоиска() Экспорт
	
	Возврат КлючПодсистемы();
	
КонецФункции

// Ключ подсистемы.
// 
// Возвращаемое значение:
//  Строка
Функция КлючПодсистемы() Экспорт
	
	Возврат "РГП_РасширенныйГлобальныйПоиск";
	
КонецФункции

// Необходимо ли переданный ЭлементРезультата обрабатывать методами расширения
// 
// Параметры:
//  ЭлементРезультата - ЭлементРезультатаГлобальногоПоиска
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоЭлементРезультатаРасширенногоГлобальногоПоиска(ЭлементРезультата) Экспорт
	
	Возврат ЭлементРезультата.ВидПоиска = КлючВидаРасширенногоГлобальногоПоиска();
	
КонецФункции

// Добавляет элемент результата глобального поиска.
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска
//  ВыполняемоеДействие - Произвольный
//                      - см. ВыполняемоеДействие
//  Представление - Строка, ФорматированнаяСтрока - Представление
//  Картинка - Неопределено, Картинка - Картинка
//  Описание - Строка - Описание
Процедура ДобавитьЭлементРезультатаГлобальногоПоиска(РезультатыПоиска, 
	ВыполняемоеДействие, Представление = "", 
	Картинка = Неопределено, Описание = "") Экспорт
	
	ЭлементРезультата = ЭлементРезультатаГлобальногоПоиска(
		ВыполняемоеДействие, Представление, Картинка, Описание);
	
	РезультатыПоиска.Добавить(ЭлементРезультата);
	
КонецПроцедуры

// Новый элемент результата глобального поиска.
// 
// Параметры:
//  ВыполняемоеДействие - Произвольный
//                      - см. ВыполняемоеДействие
//  Представление - Строка - Представление
//  Картинка - Неопределено, Картинка - Картинка
//  Описание - Строка - Описание
// 
// Возвращаемое значение:
//  ЭлементРезультатаГлобальногоПоиска
Функция ЭлементРезультатаГлобальногоПоиска(ВыполняемоеДействие, 
	Представление = "", Картинка = Неопределено, Описание = "") Экспорт
	
	Возврат Новый ЭлементРезультатаГлобальногоПоиска(ВыполняемоеДействие, 
		Представление, Картинка, КлючВидаРасширенногоГлобальногоПоиска(), Описание);
	
КонецФункции

// Добавить элемент результата стандартной команды
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
//  ЭлементРезультата - ЭлементРезультатаГлобальногоПоиска
Процедура ДобавитьЭлементРезультатаСтандартнойКоманды(РезультатыПоиска, 
	ПараметрыВыполнения, ЭлементРезультата) Экспорт
	
	Если ПараметрыВыполнения.ПользовательскиеНастройки.ГруппироватьСтандартныеКоманды Тогда
		ОбщийЭлемент = РезультатыПоиска[РезультатыПоиска.Количество() - 1];
		ДобавитьДействиеНаОсновеЭлементаРезультата(ОбщийЭлемент, ЭлементРезультата);
	Иначе
		РезультатыПоиска.Добавить(ЭлементРезультата);
	КонецЕсли;
	
КонецПроцедуры

// Добавить действие на основе элемента результата.
// 
// Параметры:
//  ПолучательДействия - ЭлементРезультатаГлобальногоПоиска - Элемент результата, в который необходимо добавить действие
//  ЭлементРезультата - ЭлементРезультатаГлобальногоПоиска - Элемент результата, которое нужно перенести в действие
Процедура ДобавитьДействиеНаОсновеЭлементаРезультата(ПолучательДействия, ЭлементРезультата) Экспорт
	
	ПолучательДействия.Действия.Добавить(ЭлементРезультата.Значение, 
		ЭлементРезультата.Представление, ЭлементРезультата.Картинка);
	
КонецПроцедуры

// Добавить элемент результата стандартной команды ввода строки
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
Процедура ДобавитьЭлементРезультатаСтандартнойКоманды_ВводСтроки(РезультатыПоиска, ПараметрыВыполнения) Экспорт
	
	Элемент = ЭлементРезультатаКомандаВводаСтроки(ПараметрыВыполнения);
	
	ДобавитьЭлементРезультатаСтандартнойКоманды(РезультатыПоиска, ПараметрыВыполнения, Элемент);
	
КонецПроцедуры

// Добавить элемент результата стандартной команды обновления результатов поиска
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
Процедура ДобавитьЭлементРезультатаСтандартнойКоманды_Обновить(РезультатыПоиска, ПараметрыВыполнения) Экспорт
	
	ВыполняемоеДействие = ВыполняемоеДействиеОбновитьРезультаты(ПараметрыВыполнения);
	ЭлементРезультата = ЭлементРезультатаГлобальногоПоиска(
		ВыполняемоеДействие, СтрокаЗаголовком("Обновить"), БиблиотекаКартинок.Обновить);
	
	ДобавитьЭлементРезультатаСтандартнойКоманды(РезультатыПоиска, ПараметрыВыполнения, ЭлементРезультата);
	
КонецПроцедуры

// Добавить элемент результата стандартной команды возвращения на команду вверх
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
Процедура ДобавитьЭлементРезультатаСтандартнойКоманды_Назад(РезультатыПоиска, ПараметрыВыполнения) Экспорт
	
	ВыполняемоеДействие = ВыполняемоеДействиеПереходаНазад(ПараметрыВыполнения);
	
	ЭлементРезультата = ЭлементРезультатаГлобальногоПоиска(
		ВыполняемоеДействие, СтрокаЗаголовком("Назад"), БиблиотекаКартинок.Назад);
	
	ДобавитьЭлементРезультатаСтандартнойКоманды(
		РезультатыПоиска, ПараметрыВыполнения, ЭлементРезультата);
	
КонецПроцедуры

// Параметры обработки страниц результата команды.
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
//  КоллекцияСтрок - ТаблицаЗначений -
//  	- ЭлементыФормы -
//  	- ДеревоЗначений -
//  	- Массив из Произвольный -
//  	- СписокЗначений из Произвольный -
//  РезультатыПоиска - РезультатГлобальногоПоиска, Неопределено - Добавляется команда переключения страниц
// 
// Возвращаемое значение:
//  Структура - :
// * ПараметрыСтраниц - см. ПараметрыСтраницРезультатаКоманды
// * ВыводимыеСтроки - Массив из Произвольный
Функция ПараметрыОбработкиСтраницРезультатаКоманды(ПараметрыВыполнения, КоллекцияСтрок, РезультатыПоиска) Экспорт
	
	ПараметрыСтраниц = ПараметрыСтраницРезультатаКоманды(
		ПараметрыВыполнения, КоллекцияСтрок.Количество());
	
	ПодходящиеСтроки = Новый Массив; // Массив из Неопределено
	
	ОсталосьВывести  = ПараметрыСтраниц.ОсталосьВывести;
	Если ОсталосьВывести > 0 Тогда
		
		ДобавитьЭлементРезультатаСтандартнойКоманды_Страница(
			РезультатыПоиска, ПараметрыВыполнения, ПараметрыСтраниц);
			
		ПропускСтрок = Истина;
		НомерСтроки = 0;
		Для Каждого ТекущиеДанные Из КоллекцияСтрок Цикл // Произвольный
			
			НомерСтроки = НомерСтроки + 1;
			ПропускСтрок = ?(ПропускСтрок, ПараметрыСтраниц.ВыводитьСоСтроки > НомерСтроки, Ложь);
			Если ПропускСтрок Тогда
				Продолжить;
			КонецЕсли;
			
			ПодходящиеСтроки.Добавить(ТекущиеДанные);
		
			ОсталосьВывести = ОсталосьВывести - 1;
			Если ОсталосьВывести = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
			
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ПараметрыСтраниц", ПараметрыСтраниц);
	Результат.Вставить("ВыводимыеСтроки", ПодходящиеСтроки);
	
	Возврат Результат;
	
КонецФункции

// Параметры добавления элемента результата переключения страницы.
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
//  ВсегоРезультатов - Число
// 
// Возвращаемое значение:
//  Структура - :
// * ВсегоСтраниц - Число
// * ТекущаяСтраница - Число
// * ВсегоРезультатов - Число
// * ВыводитьСоСтроки - Число
// * ОсталосьВывести - Число
Функция ПараметрыСтраницРезультатаКоманды(ПараметрыВыполнения, Знач ВсегоРезультатов) Экспорт
	
	ПривестиЧисло(ВсегоРезультатов, 0);
	
	ТекущаяСтраница = ЗначениеПараметраКоманды_Страница(ПараметрыВыполнения);
	МаксимумСтрок = ПараметрыВыполнения.ПользовательскиеНастройки.МаксимумСтрокРезультата;
	ПривестиЧисло(МаксимумСтрок, 1);
	ВсегоСтраниц = ЦелВБольшую(ВсегоРезультатов / Макс(МаксимумСтрок, 1));
	
	Если ВсегоРезультатов = 0 Тогда
		ВыводитьСоСтроки = 0;
		ОсталосьВывести = 0;
		ТекущаяСтраница = 1;
	ИначеЕсли ТекущаяСтраница = 1 Тогда
		ВыводитьСоСтроки = 1;
		ОсталосьВывести = Мин(МаксимумСтрок, ВсегоРезультатов);
	Иначе
		ПривестиЧисло(ТекущаяСтраница, 1, ВсегоСтраниц);
		ВыводитьСоСтроки = (МаксимумСтрок * (ТекущаяСтраница - 1)) + 1;
		ОсталосьВывести = Мин(МаксимумСтрок, ВсегоРезультатов - ВыводитьСоСтроки + 1);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ВсегоСтраниц", ВсегоСтраниц);
	Результат.Вставить("ТекущаяСтраница", ТекущаяСтраница);
	Результат.Вставить("ВсегоРезультатов", ВсегоРезультатов);
	Результат.Вставить("ВыводитьСоСтроки", ВыводитьСоСтроки);
	Результат.Вставить("ОсталосьВывести", ОсталосьВывести);
	
	//@skip-check constructor-function-return-section - Баг ЕДТ
	Возврат Результат;
	
КонецФункции

// Добавить элемент результата стандартной команды страница.
// 
// Параметры:
//  РезультатыПоиска- РезультатГлобальногоПоиска
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
//  ПараметрыСтраниц - см. ПараметрыСтраницРезультатаКоманды
Процедура ДобавитьЭлементРезультатаСтандартнойКоманды_Страница(
	РезультатыПоиска, ПараметрыВыполнения, ПараметрыСтраниц) Экспорт
	
	Если ПараметрыСтраниц.ВсегоСтраниц <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтраница = ПараметрыСтраниц.ТекущаяСтраница;
	
	Представление = СтрШаблон(
		"Страница %3 из %4	|	Выведено %1 из %2", 
		ПараметрыСтраниц.ОсталосьВывести, ПараметрыСтраниц.ВсегоРезультатов, 
		ТекущаяСтраница, ПараметрыСтраниц.ВсегоСтраниц);
	
	ЭлементРезультата = ЭлементРезультатаГлобальногоПоиска(Неопределено, СтрокаЗаголовком(Представление));

	ДействиеПерваяСтраница = ВыполняемоеДействиеПереходаНаСтраницу(ПараметрыВыполнения, 1);
	ЭлементРезультата.Действия.Добавить(ДействиеПерваяСтраница, СтрокаЗаголовком("<<"));
		
	ДействиеПредыдущаяСтраница = ВыполняемоеДействиеПереходаНаСтраницу(ПараметрыВыполнения, ТекущаяСтраница - 1);
	ЭлементРезультата.Действия.Добавить(ДействиеПредыдущаяСтраница, СтрокаЗаголовком("<"));
		
	ПредставлениеСтраницы = СтрокаЗаголовком(СтрШаблон("⌠ %1 ⌡", ТекущаяСтраница));
	ДействиеВвестиСтраницу = ВыполняемоеДействиеВводНомераСтраницы(ПараметрыВыполнения, ТекущаяСтраница);
	ЭлементРезультата.Действия.Добавить(ДействиеВвестиСтраницу, ПредставлениеСтраницы);
	
	ДействиеСледующаяСтраница = ВыполняемоеДействиеПереходаНаСтраницу(ПараметрыВыполнения, ТекущаяСтраница + 1);
	ЭлементРезультата.Действия.Добавить(ДействиеСледующаяСтраница , СтрокаЗаголовком(">"));

	ДействиеПоследняяСтраница  = ВыполняемоеДействиеПереходаНаСтраницу(ПараметрыВыполнения, ПараметрыСтраниц.ВсегоСтраниц);
	ЭлементРезультата.Действия.Добавить(ДействиеПоследняяСтраница , СтрокаЗаголовком(">>"));
	
	РезультатыПоиска.Добавить(ЭлементРезультата);
	
КонецПроцедуры

// Элемент результата команда ввода строки в конце поля поиска.
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
// 
// Возвращаемое значение:
//  ЭлементРезультатаГлобальногоПоиска
Функция ЭлементРезультатаКомандаВводаСтроки(ПараметрыВыполнения) Экспорт
	
	СтрокаПоиска = ПараметрыВыполнения.ПолныйКлючСПараметрами;
	НачальнаяСтрока = ПараметрыВыполнения.СтрокаПоиска;
	
	Если ЗначениеЗаполнено(НачальнаяСтрока) 
		И НЕ ПараметрыВыполнения.ПользовательскиеНастройки.ГруппироватьСтандартныеКоманды Тогда
		Представление = СтрШаблон("Строка поиска: %1", НачальнаяСтрока);
	Иначе
		Представление = "Поиск";
	КонецЕсли;
	
	ПараметрыМетода = ПараметрыДействияВводаСтроки(СтрокаПоиска, НачальнаяСтрока, Представление);
	
	Представление = СтрокаЗаголовком(Представление);
	
	ОписаниеОповещения = ОписаниеОповещения(
		"ОтобразитьВводСтроки", "РГП_ГлобальныйПоискКлиент", ПараметрыМетода);
	ВыполняемоеДействие = ВыполняемоеДействиеОписанияОповещения(ОписаниеОповещения);
	
	ЭлементРезультата = ЭлементРезультатаГлобальногоПоиска(
		ВыполняемоеДействие, Представление, БиблиотекаКартинок.ПоискДанных);
		
	Если ЗначениеЗаполнено(НачальнаяСтрока) 
		И НЕ ПараметрыВыполнения.ПользовательскиеНастройки.ГруппироватьСтандартныеКоманды Тогда
		
		ДействиеОчистить = ВыполняемоеДействиеПереходаНаКоманду(
			ПараметрыВыполнения.ПолныйКлюч, ПараметрыВыполнения.ПараметрыКоманды);
		ЭлементРезультата.Действия.Добавить(ДействиеОчистить, "Очистить", БиблиотекаКартинок.Очистить);
		
	КонецЕсли;
	
	Возврат ЭлементРезультата;
	
КонецФункции

// Параметры действия ввода строки.
// 
// Параметры:
//  СтрокаПоиска - Строка - Строка поиска, в конце которой будет подставлена введенная строка
//  НачальнаяСтрока - Строка - Начальное значение строки, которое будет использовано в качестве начального значения в окне ввода.
//  Представление - Строка - Представление в заголовке поля ввода
// 
// Возвращаемое значение:
//  Структура - Параметры действия ввода строки:
//  * СтрокаПоиска - см. ПараметрыДействияВводаСтроки.СтрокаПоиска
//  * НачальнаяСтрока - см. ПараметрыДействияВводаСтроки.НачальнаяСтрока
//  * Представление - см. ПараметрыДействияВводаСтроки.Представление
Функция ПараметрыДействияВводаСтроки(СтрокаПоиска, НачальнаяСтрока, Представление) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("СтрокаПоиска", СтрокаПоиска);
	Параметры.Вставить("Представление", Представление);
	Параметры.Вставить("НачальнаяСтрока", НачальнаяСтрока);
	
	Возврат Параметры;
	
КонецФункции

// Стандартные команды
// 
// Возвращаемое значение:
//  Структура - Стандартные команды:
// * Назад - Строка - Команда "Назад" для возвращения к предыдущей команде
// * ВводСтроки - Строка - Команда "Ввод строки" для изменения строки поиска
// * Обновить - Строка - Команда "Обновить" для обновления результатов поиска
// * Параметры - Строка - Команда "Параметры" отображается, когда строка поиска содержит параметры
// * Избранное - Строка - Команда "Избранное" позволяет добавить\удалить команду с параметрами в избранное
// * Настройки - Строка - Команда "Настройки" позволяет изменить пользовательские настройки команды
Функция СтандартныеКоманды() Экспорт

	Команды = Новый Структура;
	Команды.Вставить("Назад", "Назад");
	Команды.Вставить("ВводСтроки", "ВводСтроки");
	Команды.Вставить("Обновить", "Обновить");
	Команды.Вставить("Параметры", "Параметры");
	Команды.Вставить("Избранное", "Избранное");
	Команды.Вставить("Настройки", "Настройки");
	
	Возврат Команды;
	
КонецФункции

// Выполняемое действие вычисления выражения.
// 
// Параметры:
//  ПолныйКлючКоманды - Строка
//  НаСервере - Булево
//  НачальноеВыражение - Строка
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействиеОписанияОповещения
Функция ВыполняемоеДействиеВычисленияВыражения(ПолныйКлючКоманды, НаСервере, НачальноеВыражение = "") Экспорт
	
	ИмяМодуля = "РГП_ГлобальныйПоискКлиент";
	ИмяМетода = "ГлобальныйПоискВычислитьВыражение";
	
	ПараметрыОповещения = ДополнительныеПараметрыВыполненияКомандыВычисленияВыражения(
		НаСервере, НачальноеВыражение, ПолныйКлючКоманды);
		
	ОписаниеОповещения  = ОписаниеОповещения(ИмяМетода, ИмяМодуля, ПараметрыОповещения);
	
	Возврат ВыполняемоеДействиеОписанияОповещения(ОписаниеОповещения);
	
КонецФункции

// Дополнительные параметры выполнения команды вычисления выражения.
// 
// Параметры:
//  НаСервере - Булево
//  НачальноеВыражение - Строка
//  ПолныйКлючКоманды - Строка
// 
// Возвращаемое значение:
//  Структура:
// * НаСервере - Булево
// * НачальноеВыражение - Строка
// * ПолныйКлючКоманды - Строка
Функция ДополнительныеПараметрыВыполненияКомандыВычисленияВыражения(
	НаСервере, НачальноеВыражение, ПолныйКлючКоманды) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НаСервере", НаСервере);
	Результат.Вставить("НачальноеВыражение", НачальноеВыражение);
	Результат.Вставить("ПолныйКлючКоманды" , ПолныйКлючКоманды);
	
	Возврат Результат;
	
КонецФункции

// Элемент истории вычисления выражения.
// 
// Возвращаемое значение:
//  Структура -  Элемент истории вычисления выражения:
// * НаСервере - Булево - Признак вычисления на сервере
// * Выражение - Строка - Текст выражения
// * ОписаниеОшибки - Строка
// * Успешно - Булево - Успешно ли выполнилось выражение или упало в ошибку
// * ПредставлениеТипа - Строка - Тип результата строкой
// * Результат - Неопределено, Произвольный - Результат выражения
// * Представление - Строка
// * Дата - Дата - Дата последнего использования
Функция ЭлементИсторииВычисленияВыражения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НаСервере", Ложь);
	Результат.Вставить("Выражение", "");
	Результат.Вставить("Успешно", Ложь);
	Результат.Вставить("ОписаниеОшибки", "");
	Результат.Вставить("ПредставлениеТипа", "");
	Результат.Вставить("Результат", Неопределено);
	Результат.Вставить("Представление", "");
	Результат.Вставить("Дата", '0001 01 01');
	
	Возврат Результат;
	
КонецФункции

// Параметры выполнения команды очистки настроек.
// 
// Возвращаемое значение:
//  Структура - :
// * КлючКоманды - Строка - Полный ключ команды, настройки которой необходимо очистить
// * Пользователь - Строка, Неопределено - Если заполнено, то очистка для конкретного пользователя
Функция ПараметрыВыполненияКомандыОчисткиНастроек() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КлючКоманды", "");
	Результат.Вставить("Пользователь", Неопределено);
	
	Возврат Результат;
	
КонецФункции

#Область ПользовательскиеНастройки

// Новые пользовательские настройки.
// 
// Возвращаемое значение:
//  Структура - :
// * ОбщиеНастройки - см. НовыеОбщиеПользовательскиеНастройки
// * НастройкиКоманд - см. НоваяКоллекцияПользовательскихНастроекКоманд
Функция НоваяКоллекцияПользовательскихНастроек() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОбщиеНастройки", НовыеОбщиеПользовательскиеНастройки());
	Результат.Вставить("НастройкиКоманд", НоваяКоллекцияПользовательскихНастроекКоманд());
	
	Возврат Результат;
	
КонецФункции

// Новые пользовательские настройки.
// 
// Возвращаемое значение:
//  Структура - :
// * ГруппироватьСтандартныеКоманды - Булево - Требуется ли группировать стандартные команды в одном пункте меню
// * МаксимумСтрокРезультата - Число - Максимальное число строк результата
Функция НовыеОбщиеПользовательскиеНастройки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ГруппироватьСтандартныеКоманды", Истина);
	Результат.Вставить("МаксимумСтрокРезультата", 50);
	
	Возврат Результат;
	
КонецФункции

// Новая коллекция пользовательских настроек команд.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - Полный ключ команды
//  * Значение - см. НоваяКоллекцияПользовательскихНастроекКоманды
Функция НоваяКоллекцияПользовательскихНастроекКоманд() Экспорт
	
	Возврат Новый Соответствие;
	
КонецФункции

// Новая коллекция пользовательских настроек команды.
// 
// Возвращаемое значение:
//  Структура
Функция НоваяКоллекцияПользовательскихНастроекКоманды() Экспорт
	
	Возврат Новый Структура;
	
КонецФункции

// Новая коллекция описаний доступных пользовательских настроек.
// 
// Возвращаемое значение:
//  Структура
Функция НоваяКоллекцияОписанийДоступныхПользовательскихНастроек() Экспорт
	
	Возврат Новый Структура;
	
КонецФункции

// Описания доступных пользовательских настроек.
// 
// Возвращаемое значение:
//  Структура из КлючИЗначение:
//  * Ключ - Строка
//  * Значение - см. НовыйОписаниеПользовательскойНастройки
Функция ОписанияДоступныхПользовательскихНастроек() Экспорт
	
	Результат = НоваяКоллекцияОписанийДоступныхПользовательскихНастроек();
	
	ТекущаяНастройка = НовыйОписаниеПользовательскойНастройки();
	ТекущаяНастройка.Ключ = "ГруппироватьСтандартныеКоманды";
	ТекущаяНастройка.Представление = "Группировать стандартные команды";
	ТекущаяНастройка.Пояснение = "Стоит ли группировать стандартные команды в одном элементе";
	ТекущаяНастройка.Тип = Новый ОписаниеТипов("Булево");
	ТекущаяНастройка.ДоступноАвто = Истина;
	ТекущаяНастройка.ФорматнаяСтрока = "БЛ='Не группировать'; БИ=Группировать;";
	Результат.Вставить(ТекущаяНастройка.Ключ, ТекущаяНастройка);
	
	ТекущаяНастройка = НовыйОписаниеПользовательскойНастройки();
	ТекущаяНастройка.Ключ = "МаксимумСтрокРезультата";
	ТекущаяНастройка.Представление = "Максимум строк результата";
	ТекущаяНастройка.Пояснение = "Какое максимальное число строк результата выводить";
	ТекущаяНастройка.Тип = Новый ОписаниеТипов("Число", , ,
		Новый КвалификаторыЧисла(5, 0, ДопустимыйЗнак.Неотрицательный));
	Результат.Вставить(ТекущаяНастройка.Ключ, ТекущаяНастройка);
	
	Возврат Результат;
	
КонецФункции

// Описание пользовательской настройки.
// 
// Параметры:
//  ОписанияНастроек - см. ОписанияДоступныхПользовательскихНастроек
//  КлючНастройки - Строка
//  ВызыватьИсключение - Булево
// 
// Возвращаемое значение:
//  см. НовыйОписаниеПользовательскойНастройки
Функция ОписаниеПользовательскойНастройки(ОписанияНастроек, КлючНастройки, ВызыватьИсключение = Истина) Экспорт
	
	ОписаниеНастройки = Неопределено; // см. НовыйОписаниеПользовательскойНастройки
	ОписанияНастроек.Свойство(КлючНастройки, ОписаниеНастройки);
	Если ОписаниеНастройки = Неопределено И ВызыватьИсключение Тогда
		ВызватьИсключение "Не найдено описание пользовательской настройки: " + КлючНастройки;
	Иначе
		//@skip-check constructor-function-return-section - баг ЕДТ
		Возврат ОписаниеНастройки;
	КонецЕсли;
	
КонецФункции

// Приводит переданное значение в соответствии с описанием пользовательской настройки.
// 
// Параметры:
//  Значение - Произвольный - Проверяемое значение
//  ОписаниеНастройки - см. НовыйОписаниеПользовательскойНастройки
//  ЗначениеИзменено - Булево - Было ли изменено значение
// 
Процедура ПривестиЗначениеКПользовательскойНастройке(Значение, Знач ОписаниеНастройки, ЗначениеИзменено = Ложь) Экспорт
	
	Если ОписаниеНастройки.ДоступноАвто И Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовоеЗначение = ОписаниеНастройки.Тип.ПривестиЗначение(Значение);
	Если НЕ ЗначениеПользовательскойНастройкиЕстьВДоступных(ОписаниеНастройки, НовоеЗначение) Тогда
		НовоеЗначение = ОписаниеНастройки.ЗначениеПоУмолчанию;
	КонецЕсли;
	
	ЗначениеИзменено = НовоеЗначение <> Значение;
	Если ЗначениеИзменено Тогда
		Значение = НовоеЗначение;
	КонецЕсли;
	
КонецПроцедуры

// Значение пользовательской настройки есть в доступных.
// 
// Параметры:
//  ОписаниеНастройки - см. НовыйОписаниеПользовательскойНастройки
//  Значение - Произвольный
// 
// Возвращаемое значение:
//  Булево
Функция ЗначениеПользовательскойНастройкиЕстьВДоступных(ОписаниеНастройки, Значение) Экспорт
	
	Если ЗначениеЗаполнено(ОписаниеНастройки.ДоступныеЗначения) Тогда
		
		ЗначениеДоступно = ОписаниеНастройки.ДоступныеЗначения.НайтиПоЗначению(Значение) <> Неопределено;
		
	ИначеЕсли Значение = Неопределено Тогда
		
		ЗначениеДоступно = ОписаниеНастройки.ДоступноАвто;
		
	Иначе
		
		ЗначениеДоступно = Истина;
		
	КонецЕсли;
	
	Возврат ЗначениеДоступно;
	
КонецФункции

// Представление значения пользовательской настройки.
// 
// Параметры:
//  ОписаниеНастройки - см. ОписаниеПользовательскойНастройки
//  Значение - Булево, Число, Дата, Строка, Произвольный - Значение настройки
// 
// Возвращаемое значение:
//   Строка
Функция ПредставлениеЗначенияПользовательскойНастройки(ОписаниеНастройки, Значение) Экспорт
	
	Если ОписаниеНастройки.ДоступноАвто И Значение = Неопределено Тогда
		
		Представление = ПредставлениеЗначенияАвтоПользовательскойНастройки();
		
	Иначе
		
		Представление = Формат(Значение, ОписаниеНастройки.ФорматнаяСтрока);
		
		Если ЗначениеЗаполнено(ОписаниеНастройки.ДоступныеЗначения) Тогда
			ДоступноеЗначение  = ОписаниеНастройки.ДоступныеЗначения.НайтиПоЗначению(Значение);
			Если ДоступноеЗначение <> Неопределено Тогда
				Представление = ДоступноеЗначение.Представление;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Представление;

КонецФункции

// Новый описание пользовательской настройки.
// 
// Возвращаемое значение:
//  Структура - :
// * Ключ - Строка - Ключ настройки
// * Представление - Строка -  Представление для пользователя
// * Пояснение - Строка -  Расширенное представление для интерфейса настройки
// * Картинка - Картинка
// * ЗначениеПоУмолчанию - Неопределено, Произвольный - Значение по умолчанию
// * Тип - ОписаниеТипов - Тип значения
// * ДоступныеЗначения - СписокЗначений из Произвольный - Доступные значения
// * ФорматнаяСтрока - Строка - Форматная строка представления значения
// * ДоступноАвто - Булево - Доступность значения "Авто" (Неопределено)
Функция НовыйОписаниеПользовательскойНастройки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ключ", "");
	Результат.Вставить("Представление", "");
	Результат.Вставить("Пояснение", "");
	Результат.Вставить("Картинка", БиблиотекаКартинок.Параметры);
	Результат.Вставить("ЗначениеПоУмолчанию", Неопределено);
	Результат.Вставить("Тип", Новый ОписаниеТипов);
	Результат.Вставить("ДоступныеЗначения", Новый СписокЗначений());
	Результат.Вставить("ФорматнаяСтрока", "");
	Результат.Вставить("ДоступноАвто", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Представление значения авто пользовательской настройки.
// 
// Возвращаемое значение:
//  Строка
Функция ПредставлениеЗначенияАвтоПользовательскойНастройки() Экспорт
	
	Возврат "Авто";
	
КонецФункции

// Ключ пользовательской настройки - Поиск метаданных - Варианты отчетов.
// 
// Возвращаемое значение:
//  Строка
Функция КлючПользовательскойНастройки_ПоискМетаданных_ВариантыОтчетов() Экспорт
	
	Возврат "ПоискВариантовОтчетов";
	
КонецФункции

// Обработать значения пользовательских настроек.
// 
// Параметры:
//  КоллекцияНастроек - Структура
//  ОписанияНастроек - см. НоваяКоллекцияОписанийДоступныхПользовательскихНастроек
Процедура ОбработатьЗначенияПользовательскихНастроек(КоллекцияНастроек, ОписанияНастроек) Экспорт
	
	Для Каждого КлючИЗначение Из КоллекцияНастроек Цикл
		
		КлючНастройки = КлючИЗначение.Ключ;
		ТекущееЗначение = КлючИЗначение.Значение;
		
		ОписаниеНастройки = ОписаниеПользовательскойНастройки(
			ОписанияНастроек, КлючНастройки, Ложь);
		Если ОписаниеНастройки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеИзменилось = Ложь;
		ПривестиЗначениеКПользовательскойНастройке(
			ТекущееЗначение, ОписаниеНастройки, ЗначениеИзменилось);
		
		Если ЗначениеИзменилось Тогда
			КоллекцияНастроек[КлючНастройки] = ТекущееЗначение;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыКоманд

// Добавить доступный параметр команды.
// 
// Параметры:
//  Коллекция - см. НоваяКоллекцияПараметровКоманды
//  Параметр - см. ОписаниеПараметраКоманды
// 
Процедура ДобавитьДоступныйПараметрКоманды(Коллекция, Параметр) Экспорт
	
	Коллекция.Вставить(Параметр.Ключ, Параметр);
	
КонецПроцедуры

// Коллекция параметров команды.
// 
// Возвращаемое значение:
//  Структура из КлючИЗначение :
// * Ключ - Строка - Ключ параметра
// * Значение - см. ОписаниеПараметраКоманды
Функция НоваяКоллекцияПараметровКоманды() Экспорт
	
	Возврат Новый Структура;
	
КонецФункции

// Описание параметра команды.
// 
// Параметры:
//  Ключ - Строка - Ключ параметра
//  Представление - Строка - Представление параметра
//  Тип - см. ОписаниеТипов.Тип
//  МетодПредставления - Неопределено - Если указан, то при формировании представления параметра будет вызыван метод
//                     - см. ОписаниеОповещения
//   
// Возвращаемое значение:
//  Структура:
// * Ключ - Строка - Ключ
// * Представление - Строка - Представление
// * Тип - ОписаниеТипов - Ограничение типа
// * Отображать - Булево - Выводить ли значение команды в интерфейсе 
// * МетодПредставления - см. ОписаниеПараметраКоманды.МетодПредставления
Функция ОписаниеПараметраКоманды(Ключ, Представление = "", 
	Тип = Неопределено, МетодПредставления = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ключ", Ключ);
	Результат.Вставить("Представление", Представление);
	Результат.Вставить("Тип", ОписаниеТипов(Тип));
	Результат.Вставить("Отображать", Истина);
	Результат.Вставить("МетодПредставления", МетодПредставления);
	
	Возврат Результат;
	
КонецФункции

// Параметры метода представления параметра.
// 
// Параметры:
//  Ключ - Строка
//  Значение - Произвольный
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
// 
// Возвращаемое значение:
//  Структура:
// * Значение - см. ПараметрыМетодаПредставленияПараметра.Значение
// * Ключ - см. ПараметрыМетодаПредставленияПараметра.Ключ
// * ПараметрыВыполнения - см. ПараметрыМетодаПредставленияПараметра.ПараметрыВыполнения
// * Представление - Строка
Функция ПараметрыМетодаПредставленияПараметра(Ключ, Значение, ПараметрыВыполнения) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Значение", Значение);
	Результат.Вставить("Ключ", Ключ);
	Результат.Вставить("ПараметрыВыполнения", ПараметрыВыполнения);
	Результат.Вставить("Представление", "");
	
	Возврат Результат;
	
КонецФункции

// Описание параметра команды - Текущая страница.
// 
// Возвращаемое значение:
//  см. ОписаниеПараметраКоманды
Функция ОписаниеПараметраКоманды_Страница() Экспорт
	
	КлючПараметра = КлючПараметраКоманды_Страница();
	
	ОписаниеПараметра = ОписаниеПараметраКоманды(КлючПараметра, "Страница", ОписаниеТипаПараметраКоманды_Страница());
	ОписаниеПараметра.Отображать = Ложь; // Выводится отдельно см. ДобавитьЭлементРезультатаСтандартнойКоманды_Страница
	
	Возврат ОписаниеПараметра;
	
КонецФункции

// Значение параметра команды страница.
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
// 
// Возвращаемое значение:
//  Число
Функция ЗначениеПараметраКоманды_Страница(ПараметрыВыполнения) Экспорт
	
	ТекущаяСтраница = Неопределено; // Число
	ПараметрыВыполнения.ПараметрыКоманды.Свойство(КлючПараметраКоманды_Страница(), ТекущаяСтраница);
	ПривестиЧисло(ТекущаяСтраница, 1);
	
	Возврат ТекущаяСтраница;
	
КонецФункции

// Ключ параметра команды страница.
// 
// Возвращаемое значение:
//  Строка
Функция КлючПараметраКоманды_Страница() Экспорт
	
	Возврат "С";
	
КонецФункции

// Описание типа параметра команды страница.
// 
// Возвращаемое значение:
//  ОписаниеТипов
Функция ОписаниеТипаПараметраКоманды_Страница() Экспорт
	
	Возврат Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный));
	
КонецФункции

// Описание параметра команды - Тип.
// 
// Возвращаемое значение:
//  см. ОписаниеПараметраКоманды
Функция ОписаниеПараметраКоманды_Тип() Экспорт
	
	КлючПараметра = КлючПараметрКоманды_Тип();
	
	ОписаниеПараметра = ОписаниеПараметраКоманды(КлючПараметра, "Тип", "Строка");
	
	Возврат ОписаниеПараметра;
	
КонецФункции

// Значение параметра команды - Тип.
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
// 
// Возвращаемое значение:
//  Число
Функция ЗначениеПараметраКоманды_Тип(ПараметрыВыполнения) Экспорт
	
	Значение = Неопределено; // Строка
	ПараметрыВыполнения.ПараметрыКоманды.Свойство(КлючПараметрКоманды_Тип(), Значение);
	Если ТипЗнч(Значение) <> Тип("Строка") Тогда
		Значение = "";
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Значение параметра команды любой тип.
// 
// Возвращаемое значение:
//  Строка
Функция ЗначениеПараметраКоманды_ЛюбойТип() Экспорт
	
	Возврат "Все";
	
КонецФункции

// Ключ параметр команды тип.
// 
// Возвращаемое значение:
//  Строка
Функция КлючПараметрКоманды_Тип() Экспорт
	
	Возврат "Тип";
	
КонецФункции

// Ключ параметра команды Настройки - Команда.
// 
// Возвращаемое значение:
//  Строка
Функция КлючПараметраКоманды_Настройки_Команда() Экспорт
	
	Возврат "Команда";
	
КонецФункции

#КонецОбласти

#Область Оформление

// Строка шрифтом заголовка.
// 
// Параметры:
//  Строка - Строка, Массив из Строка - Содержимое представления
// 
// Возвращаемое значение:
//  ФорматированнаяСтрока
Функция СтрокаЗаголовком(Строка) Экспорт
	
	Возврат Новый ФорматированнаяСтрока(Строка, ШрифтЗаголовковМеню(), ЦветЗаголовковМеню());
	
КонецФункции

// Шрифт заголовков меню.
// 
// Возвращаемое значение:
//  Шрифт
Функция ШрифтЗаголовковМеню() Экспорт
	
	Возврат ВажнаяНадписьШрифт();
	
КонецФункции

// Жирный дополнительной информации.
// 
// Возвращаемое значение:
//  Шрифт
Функция ШрифтДопИнформации() Экспорт
	
	Возврат МелкийШрифтТекста();
	
КонецФункции

// Мелкий шрифт текста.
// 
// Возвращаемое значение:
//  Шрифт
Функция МелкийШрифтТекста() Экспорт
	
	Возврат ШрифтСтиля("МелкийШрифтТекста");
	
КонецФункции

// Важная надпись шрифт.
// 
// Возвращаемое значение:
//  Шрифт
Функция ВажнаяНадписьШрифт() Экспорт
	
	Возврат ШрифтСтиля("ВажнаяНадписьШрифт");
	
КонецФункции

// Шрифт стиля.
// 
// Параметры:
//  ИмяШрифта - Строка
// 
// Возвращаемое значение:
//  Шрифт
Функция ШрифтСтиля(ИмяШрифта) Экспорт
	
	#Если Клиент Тогда
		Возврат РГП_ГлобальныйПоискКлиент.ШрифтСтиля(ИмяШрифта);
	#Иначе
		Возврат РГП_ГлобальныйПоискВызовСервера.ШрифтСтиля(ИмяШрифта);
	#КонецЕсли
	
КонецФункции

// Цвет особого текста.
// 
// Возвращаемое значение:
//  Цвет
Функция ЦветОсобогоТекста() Экспорт
	
	Возврат ЦветВажного();
	
КонецФункции

// Цвет фона выделения.
// 
// Возвращаемое значение:
//  Цвет
Функция ЦветФонаВыделения() Экспорт
	
	Возврат ЦветФонаВыделенияПоля();
	
КонецФункции

// Цвет особого текста.
// 
// Возвращаемое значение:
//  Цвет
Функция ЦветЗаголовковМеню() Экспорт
	
	Возврат ЦветТекстаШапкиТаблицы();
	
КонецФункции

// Цвет фона выделения поля.
// 
// Возвращаемое значение:
//  Цвет
Функция ЦветФонаВыделенияПоля() Экспорт
	
	Возврат ЦветСтиля("ЦветФонаВыделенияПоля");
	
КонецФункции

// Цвет важного.
// 
// Возвращаемое значение:
//  Цвет
Функция ЦветВажного() Экспорт
	
	Возврат ЦветСтиля("ЦветВажного");
	
КонецФункции

// Цвет текста шапки.
// 
// Возвращаемое значение:
//  Цвет
Функция ЦветТекстаШапкиТаблицы() Экспорт
	
	Возврат ЦветСтиля("ЦветТекстаШапкиТаблицы");
	
КонецФункции

// Цвет стиля.
// 
// Параметры:
//  ИмяЦвета - Строка
// 
// Возвращаемое значение:
//  Цвет
Функция ЦветСтиля(ИмяЦвета) Экспорт
	
	#Если Клиент Тогда
		Возврат РГП_ГлобальныйПоискКлиент.ЦветСтиля(ИмяЦвета);
	#Иначе
		Возврат РГП_ГлобальныйПоискВызовСервера.ЦветСтиля(ИмяЦвета);
	#КонецЕсли
	
КонецФункции

// Перенести действия результатов поиска по строкам.
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска - 
//  МаксимальнаяДлинаСтроки - Число - Максимальная длина строки действий (включая команды)
//  ДлинаКоманды - Число - Длина команды (иконка + отступы)
Процедура ПеренестиДействияРезультатовПоискаПоСтрокам(РезультатыПоиска, 
	МаксимальнаяДлинаСтроки = 40, ДлинаКоманды = 4) Экспорт
	
	Для Каждого ЭлементРезультата Из РезультатыПоиска Цикл
		
		ПеренестиДействияЭлементаРезультатаПоискаПоСтрокам(
			ЭлементРезультата, МаксимальнаяДлинаСтроки, ДлинаКоманды);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ЭлементРезультата - ЭлементРезультатаГлобальногоПоиска
//  МаксимальнаяДлинаСтроки - см. ПеренестиДействияРезультатовПоискаПоСтрокам.МаксимальнаяДлинаСтроки
//  ДлинаКоманды - см. ПеренестиДействияРезультатовПоискаПоСтрокам.ДлинаКоманды
Процедура ПеренестиДействияЭлементаРезультатаПоискаПоСтрокам(ЭлементРезультата, 
	МаксимальнаяДлинаСтроки = 40, ДлинаКоманды = 4) Экспорт
	
	КоличествоСтрок = 1;
	МелкийТекстОтступа = МелкийШрифтТекста();
	КоличествоКомандНаСтроке = 0;
	ОсталосьСимволовСтроки = МаксимальнаяДлинаСтроки;

	ПрошлоеДействие = Неопределено; // ДействиеЭлементаРезультатаГлобальногоПоиска
	Для Каждого Действие Из ЭлементРезультата.Действия Цикл
		
		КоличествоКомандНаСтроке = КоличествоКомандНаСтроке + 1;
		
		ОсталосьСимволовСтроки = ОсталосьСимволовСтроки - ДлинаКоманды - СтрДлина(Действие.Текст);
		Если ОсталосьСимволовСтроки < 0 И КоличествоКомандНаСтроке > 1 Тогда
			КоличествоСтрок = КоличествоСтрок + 1;
			ОсталосьСимволовСтроки = МаксимальнаяДлинаСтроки;
			ПрошлоеДействие.Текст = Новый ФорматированнаяСтрока(
				ПрошлоеДействие.Текст, 
				Символы.ПС, 
				Новый ФорматированнаяСтрока(Символы.ПС, МелкийТекстОтступа));
		КонецЕсли;
		
		ПрошлоеДействие = Действие;
		
	КонецЦикла;
	
	Если КоличествоСтрок > 1 Тогда
		// Пустое действие для отступа, чтобы следующие строки были в один уровень
		ЭлементРезультата.Действия.Вставить(0);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Вспомогательные

// Проверяет доступность хотя бы одной роли с учетом данных в кэше
// Сначала проверяем все роли из кэша. Если ролей нет в кэше, то запрашиваем их, помещаем в кэш и проверяем снова
// 
// Параметры:
//  Параметры - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
//  ИменаРолей - см. МассивИменРолейИзПараметра.ИменаРолей
// 
// Возвращаемое значение:
//  Булево
Функция РолиДоступны(Параметры, ИменаРолей) Экспорт
	
	Если ЭтоПолноправныйПользователь(Параметры) Тогда
		Возврат Истина;
	КонецЕсли;
	
	РолиПользователя = Параметры.КэшированныеДанные.РолиПользователя;
	ПолучаемыеРоли = Новый Массив; // Массив из Строка
	
	КоллекцииРолей = Новый Массив; // Массив из Массив
	КоллекцииРолей.Добавить(МассивИменРолейИзПараметра(ИменаРолей));
	КоллекцииРолей.Добавить(ПолучаемыеРоли);
	
	Для НомерКоллекции = 1 По КоллекцииРолей.Количество() Цикл
		
		ЭтоПерваяКоллекция = (НомерКоллекции = 1);
		КоллекцияРолей     = КоллекцииРолей.Получить(НомерКоллекции - 1);
		
		Для Каждого ИмяРоли Из КоллекцияРолей Цикл // Строка
			
			РольДоступна = РолиПользователя.Получить(ИмяРоли);
			Если РольДоступна = Истина Тогда
				Возврат Истина;
			ИначеЕсли РольДоступна = Неопределено И ЭтоПерваяКоллекция Тогда
				ПолучаемыеРоли.Добавить(ИмяРоли);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЭтоПерваяКоллекция Тогда
			ДополнитьКэшРолейПользователей(Параметры, ПолучаемыеРоли);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет доступность права пользователя
// Сначала проверяем все роли из кэша. Если ролей нет в кэше, то запрашиваем их, помещаем в кэш и проверяем снова
// 
// Параметры:
//  Параметры - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
//  ИмяПрава - Строка
//  ИмяМетаданного - Строка
// 
// Возвращаемое значение:
//  Булево
Функция ЕстьПравоДоступа(Параметры, ИмяПрава, ИмяМетаданного = "") Экспорт
	
	Если ЭтоПолноправныйПользователь(Параметры) Тогда
		Возврат Истина;
	КонецЕсли;
	
	ПраваПользователя = Параметры.КэшированныеДанные.ПраваПользователя;
	КлючПоискаПрава   = КлючКэшаПоискаПоПравамПользователей(ИмяПрава, ИмяМетаданного);
	ЕстьПравоДоступа  = ПраваПользователя.Получить(КлючПоискаПрава);
	Если ЕстьПравоДоступа = Неопределено Тогда
		ДополнитьКэшПравПользователей(Параметры, ИмяПрава, ИмяМетаданного);
		ЕстьПравоДоступа = ПраваПользователя.Получить(КлючПоискаПрава);
	КонецЕсли;
	
	Возврат ЕстьПравоДоступа;
	
КонецФункции

// Дополняет кэш доступности ролей пользователей
// 
// Параметры:
//  Параметры - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
//  ИменаРолей - см. МассивИменРолейИзПараметра.ИменаРолей
// 
Процедура ДополнитьКэшРолейПользователей(Параметры, ИменаРолей) Экспорт
	
	МассивРолей = МассивИменРолейИзПараметра(ИменаРолей);
	// @skip-check unknown-method-property - Баг ЕДТ
	ДополнениеКэша = РГП_ГлобальныйПоискВызовСервера.ДоступностьРолейПользователя(МассивРолей);
	Для Каждого КлючИЗначение Из ДополнениеКэша Цикл
		Параметры.КэшированныеДанные.РолиПользователя.Вставить(ВРег(КлючИЗначение.Ключ), КлючИЗначение.Значение);
	КонецЦикла;
	
КонецПроцедуры

// Дополняет кэш доступности прав пользователей
// 
// Параметры:
//  Параметры - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
//  ИмяПрава - Строка
//  ИмяМетаданного - Строка
// 
Процедура ДополнитьКэшПравПользователей(Параметры, ИмяПрава, ИмяМетаданного = "") Экспорт
	
	// @skip-check unknown-method-property - Баг ЕДТ
	ЕстьПравоДоступа  = РГП_ГлобальныйПоискВызовСервера.ЕстьПравоДоступа(ИмяПрава, ИмяМетаданного);
	КлючПоискаПрава   = КлючКэшаПоискаПоПравамПользователей(ИмяПрава, ИмяМетаданного);
	Параметры.КэшированныеДанные.ПраваПользователя.Вставить(КлючПоискаПрава, ЕстьПравоДоступа);
	
КонецПроцедуры

// Массив имен ролей из параметра.
// 
// Параметры:
//  ИменаРолей - Строка, Массив из Строка - Массив имен или строка, разделенная запятыми или переносами строк
// 
// Возвращаемое значение:
//  Массив из Строка
Функция МассивИменРолейИзПараметра(ИменаРолей) Экспорт
	
	Если ТипЗнч(ИменаРолей) = Тип("Строка") Тогда
		Результат = СтрРазделить(ИменаРолей, "," + Символы.ПС, Ложь);
	ИначеЕсли ТипЗнч(ИменаРолей) = Тип("Массив") Тогда
		Результат = ИменаРолей;
	Иначе
		ВызватьИсключение СтрШаблон("Передан параметр некорректного типа %1", ТипЗнч(ИменаРолей));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Это полноправный пользователь.
// 
// Параметры:
//  Параметры - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоПолноправныйПользователь(Параметры) Экспорт
	
	Возврат Параметры.КэшированныеДанные.ЭтоПолноправныйПользователь;
	
КонецФункции

// Вычислить выражение.
// 
// Параметры:
//  ТекстВыражения - Строка
//  НаСервере - Булево
// 
// Возвращаемое значение:
//  см. РГП_ГлобальныйПоискКлиентСервер.ЭлементИсторииВычисленияВыражения
Функция ВычислитьВыражение(ТекстВыражения, НаСервере = Ложь) Экспорт
	
	Результат = ЭлементИсторииВычисленияВыражения();
	
	Если НаСервере Тогда
		
		// @skip-check unknown-method-property - Баг ЕДТ
		РезультатСервер = РГП_ГлобальныйПоискВызовСервера.ВычислитьВыражение(ТекстВыражения);
		ЗаполнитьЗначенияСвойств(Результат, РезультатСервер);
		Результат.НаСервере = Истина;
		
	Иначе
		
		Результат.Выражение = ТекстВыражения;
		Результат.Дата = РГП_ГлобальныйПоискВызовСервера.ТекущаяДатаСервера();
	
		// @skip-check server-execution-safe-mode - в данном случае весь смысл выполнить выражение без ограничений
		Попытка
			Значение = Вычислить(ТекстВыражения); // BSLLS:ExecuteExternalCodeInCommonModule-off
			Результат.Представление = Строка(Значение);
			Результат.ПредставлениеТипа = Строка(ТипЗнч(Значение));
			#Если Сервер ИЛИ ТолстыйКлиентУправляемоеПриложение Тогда
				Попытка
					Результат.Результат = ПоместитьВоВременноеХранилище(Значение);
				Исключение
					Результат.Результат = Результат.Представление;
				КонецПопытки;
			#Иначе
				Результат.Результат = Значение;
			#КонецЕсли
			Результат.Успешно = Истина;
		Исключение
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Если ТипЗнч(ИнформацияОбОшибке.Причина) = Тип("ИнформацияОбОшибке") Тогда
				ИнформацияОбОшибке = ИнформацияОбОшибке.Причина;
			КонецЕсли;
			Результат.ОписаниеОшибки = ИнформацияОбОшибке.Описание;
			ЛишнееНачало = "{<Неизвестный модуль>(1,1)}: ";
			Если СтрНачинаетсяС(Результат.ОписаниеОшибки, ЛишнееНачало) Тогда
				Результат.ОписаниеОшибки = Сред(Результат.ОписаниеОшибки, СтрДлина(ЛишнееНачало) + 1);
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Обрезать строку.
// 
// Параметры:
//  Строка - Строка - Строка, которую нужно обрезать, если ее длина превышает допустимую
//  МаксимальнаяДлина - Число - Максимально допустимая длина строки
Процедура ОбрезатьСтроку(Строка, Знач МаксимальнаяДлина) Экспорт
	
	Если СтрДлина(Строка) > МаксимальнаяДлина Тогда
		ОкончаниеСтроки = "...";
		Строка = Лев(Строка, МаксимальнаяДлина - СтрДлина(ОкончаниеСтроки)) + ОкончаниеСтроки;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет строку на соответствие формату имени метода (вызывает исключение).
// 
// Параметры:
//  ИмяМетода - Строка
Процедура ПроверитьИмяМетода(ИмяМетода) Экспорт
	
	ЭтоИмяМетода(ИмяМетода);
	
КонецПроцедуры

// Проверяет строку на соответствие формату имени метода.
// 
// Параметры:
//  ИмяМетода - Строка
//  ВызыватьИсключение - Булево
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоИмяМетода(ИмяМетода, ВызыватьИсключение = Истина) Экспорт
	
	Попытка
		ВременнаяСтруктура = Новый Структура;
		ВременнаяСтруктура.Вставить(ИмяМетода);
		Возврат Истина;
	Исключение
		Если ВызыватьИсключение Тогда
			ВызватьИсключение "Значение не соответствует имени метода: " + ИмяМетода;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецПопытки;
	
КонецФункции

// Заполнить структуру рекурсивно.
// 
// Параметры:
//  Приемник - Структура
//  Источник - Структура
//  ИгнорироватьНеопределено - Булево - Игнорировать ли значение Неопределено из Источника
//  Дополнять - Булево - Нужно ли дополнять несуществующими в приемнике свойствами
Процедура ЗаполнитьСтруктуруРекурсивно(Приемник, Источник, ИгнорироватьНеопределено = Ложь, Дополнять = Ложь) Экспорт
	
	ТипСтруктура = Тип("Структура");
	Если ТипЗнч(Приемник) <> ТипСтруктура 
		ИЛИ ТипЗнч(Источник) <> ТипСтруктура Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из Источник Цикл
		
		ТекущийКлюч = КлючИЗначение.Ключ;
		ЗначениеИсточника = КлючИЗначение.Значение; // Структура, Произвольный
		ЗначениеПриемника = Неопределено; 
		Если Приемник.Свойство(ТекущийКлюч, ЗначениеПриемника) Тогда
			Если ТипЗнч(ЗначениеПриемника) = Тип("Структура") Тогда
				ЗаполнитьСтруктуруРекурсивно(ЗначениеПриемника, ЗначениеИсточника, ИгнорироватьНеопределено);
			ИначеЕсли ЗначениеИсточника = Неопределено И ИгнорироватьНеопределено Тогда
				Продолжить;
			Иначе
				Приемник[ТекущийКлюч] = ЗначениеИсточника;
			КонецЕсли;
		ИначеЕсли Дополнять Тогда
			Приемник.Вставить(ТекущийКлюч, ЗначениеИсточника);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Это простой тип (строка, число, дата, булево).
// 
// Параметры:
//  Тип - Тип
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоПростойТип(Тип) Экспорт
	
	Возврат Тип = Тип("Строка") 
		ИЛИ Тип = Тип("Число")
		ИЛИ Тип = Тип("Дата")
		ИЛИ Тип = Тип("Булево"); 
	
КонецФункции

// Описание типов.
// 
// Параметры:
//  Тип - Строка, Тип, ОписаниеТипов, Неопределено - Тип(ы)
// 
// Возвращаемое значение:
//  ОписаниеТипов
Функция ОписаниеТипов(Тип) Экспорт
	
	Если Тип = Неопределено Тогда
		Возврат Новый ОписаниеТипов;
	ИначеЕсли ТипЗнч(Тип) = Тип("ОписаниеТипов") Тогда
		Возврат Тип;
	ИначеЕсли ТипЗнч(Тип) = Тип("Тип") Тогда
		Типы = Новый Массив(1);
		Типы[0] = Тип;
		Возврат Новый ОписаниеТипов(Типы);
	Иначе
		Возврат Новый ОписаниеТипов(Тип);
	КонецЕсли;
	
КонецФункции

// Значение из строки.
// 
// Параметры:
//  ОписаниеТипов - см. ОписаниеТипов.Тип
//  Строка - Строка
// 
// Возвращаемое значение:
//  Произвольный
Функция ЗначениеИзСтроки(Знач ОписаниеТипов, Знач Строка) Экспорт
	
	ОписаниеТипов = ОписаниеТипов(ОписаниеТипов);

	Значение = ОписаниеТипов.ПривестиЗначение(Строка);
	Если ЗначениеЗаполнено(Строка) И НЕ ЗначениеЗаполнено(Значение) Тогда
		Если ОписаниеТипов.СодержитТип(Тип("УникальныйИдентификатор")) Тогда
			// BSLLS:MissingCodeTryCatchEx-off
			// @skip-check empty-except-statement - Не требует обработки - пытаемся получить УИД из строки
			Попытка
				Значение = Новый УникальныйИдентификатор(Строка); // BSLLS:MissingCodeTryCatchEx-off
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Привести число.
// 
// Параметры:
//  Число - Число
//  МинимальноеЗначение - Число
//  МаксимальноеЗначение - Число
Процедура ПривестиЧисло(Число, 
	Знач МинимальноеЗначение = Неопределено, Знач МаксимальноеЗначение = Неопределено) Экспорт
	
	Если ТипЗнч(Число) <> Тип("Число")
		ИЛИ (МинимальноеЗначение <> Неопределено И Число < МинимальноеЗначение) Тогда
		Число = МинимальноеЗначение;
	ИначеЕсли МаксимальноеЗначение <> Неопределено И Число > МаксимальноеЗначение Тогда
		Число = МаксимальноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

// Структура.
// 
// Параметры:
//  Источник - Структура
//  ИсключаяКлючи - см. Массив
//  ОставляяКлючи - см. Массив
//  ПоУмолчаниюИсключать - Булево - Стоит ли по умолчанию исключать все ключи, если пустой список исключений
// 
// Возвращаемое значение:
//  Структура
Функция Структура(Знач Источник, 
	Знач ИсключаяКлючи = Неопределено, Знач ОставляяКлючи = Неопределено, 
	Знач ПоУмолчаниюИсключать = Ложь) Экспорт
	
	ИсключаяКлючи = Массив(ИсключаяКлючи); // Массив из Строка
	ОставляяКлючи = Массив(ОставляяКлючи); // Массив из Строка
	
	ЕстьИсключения = ЗначениеЗаполнено(ИсключаяКлючи);
	ЕстьОставления = ЗначениеЗаполнено(ОставляяКлючи);
	
	Результат = Новый Структура;
	
	Для Каждого КлючИЗначение Из Источник Цикл
		
		Ключ = КлючИЗначение.Ключ;
		
		Исключить = ПоУмолчаниюИсключать И НЕ ЕстьИсключения;
		
		Если НЕ Исключить И ЕстьИсключения И ЕстьВМассиве(ИсключаяКлючи, Ключ) Тогда
			Исключить = Истина;
		КонецЕсли;
		
		Если Исключить И ЕстьОставления И ЕстьВМассиве(ОставляяКлючи, Ключ) Тогда
			Исключить = Ложь;
		КонецЕсли;
		
		Если НЕ Исключить Тогда
			Результат.Вставить(Ключ, КлючИЗначение.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Массив.
// 
// Параметры:
//  Источник - Строка - Значения, разделенные запятыми или переносами строк
//           - Массив из Произвольный - Возвращаем как есть
//           - СписокЗначений из Произвольный - Возвращаем массив значений
//           - Неопределено - Возвращается неопределено 
//           - Произвольный - Массив с переданным значением
// 
// Возвращаемое значение:
//  - Массив из Произвольный 
//  - Неопределено
Функция Массив(Источник) Экспорт
	
	Если Источник = Неопределено Тогда
		Возврат Неопределено;	
	ИначеЕсли ТипЗнч(Источник) = Тип("Строка") Тогда
		Результат = СтрРазделить(Источник, "," + Символы.ПС, Ложь);
	ИначеЕсли ТипЗнч(Источник) = Тип("СписокЗначений") Тогда
		Результат = Источник.ВыгрузитьЗначения();
	ИначеЕсли ТипЗнч(Источник) = Тип("Массив") Тогда
		Результат = Источник;
	Иначе
		Результат = Новый Массив; // Массив из Неопределено
		Результат.Добавить(Источник);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Есть в массиве.
// 
// Параметры:
//  Массив - Массив из Произвольный
//  Значение - Произвольный
// 
// Возвращаемое значение:
//  Булево
Функция ЕстьВМассиве(Массив, Значение) Экспорт
	
	Возврат Массив.Найти(Значение) <> Неопределено;
	
КонецФункции

// Копия.
// 
// Параметры:
//  Значение - Структура
// 
// Возвращаемое значение:
//  Структура, Неопределено -
Функция Копия(Значение) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Структура") Тогда
		Возврат Новый Структура(Новый ФиксированнаяСтруктура(Значение));
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область Избранное

// Ключ навигационной ссылки.
// 
// Возвращаемое значение:
//  Строка
Функция КлючНавигационнойСсылки() Экспорт
	
	Возврат "ГлобальныйПоиск";
	
КонецФункции

// Параметры:
//  НавигационнаяСсылка - Строка
// 
// Возвращаемое значение:
//  Строка
Функция СтрокаПоискаИзНавигационнойСсылки(НавигационнаяСсылка) Экспорт
	
	КлючГлобальногоПоиска = КлючНавигационнойСсылки();
	
	Если СтрНачинаетсяС(НавигационнаяСсылка, КлючГлобальногоПоиска) Тогда
		Возврат Сред(НавигационнаяСсылка, СтрДлина(КлючГлобальногоПоиска) + 1);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Параметры:
//  СтрокаПоиска - Строка
// 
// Возвращаемое значение:
//  Строка
Функция НавигационнаяСсылкаСтрокиПоиска(СтрокаПоиска) Экспорт
	
	КлючНавигационнойСсылки = КлючНавигационнойСсылки();
	Возврат КлючНавигационнойСсылки + СтрокаПоиска;
	
КонецФункции

// Параметры:
//  ПолнаяСтрокаПоиска - Строка
//  Представление - Строка
// 
// Возвращаемое значение:
//  Структура - :
// * НавигационнаяСсылка - Строка - 
// * ПолнаяСтрокаПоиска - Строка - 
// * КлючПоиска - Строка - 
// * Представление - Строка - 
Функция ЭлементИзбранного(ПолнаяСтрокаПоиска = "", Представление = "") Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НавигационнаяСсылка", НавигационнаяСсылкаСтрокиПоиска(ПолнаяСтрокаПоиска));
	Результат.Вставить("ПолнаяСтрокаПоиска", ПолнаяСтрокаПоиска);
	Результат.Вставить("КлючПоиска", ВРег(ПолнаяСтрокаПоиска));
	Результат.Вставить("Представление", Представление);
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Соответствие из КлючИЗначение -:
//  * Ключ - Строка - Полная строка поиска (Врег)
//  * Значение - см. ЭлементИзбранного
Функция КоллекцияЭлементовИзбранного() Экспорт
	
	Возврат Новый Соответствие();
	
КонецФункции

// Возвращаемое значение:
//  Массив из см. ЭлементИзбранного
Функция УпорядоченнаяКоллекцияЭлементовИзбранного() Экспорт
	
	Возврат Новый Массив();
	
КонецФункции

// Параметры открытия формы элемент избранного.
// 
// Параметры:
//  ЭлементИзбранного - см. ЭлементИзбранного
// 
// Возвращаемое значение:
//  Структура:
// * ЭлементИзбранного - см. ЭлементИзбранного
Функция ПараметрыОткрытияФормы_ЭлементИзбранного(ЭлементИзбранного) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ЭлементИзбранного", ЭлементИзбранного);
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Интерфейс

// см. РГП_ГлобальныйПоискКлиентСерверПовтИсп.ФразаИнтерфейса
Функция ФразаИнтерфейса(Фраза) Экспорт
	
	Возврат РГП_ГлобальныйПоискКлиентСерверПовтИсп.ФразаИнтерфейса(Фраза);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует ключ кэша поиска по правам пользователей.
// 
// Параметры:
//  ИмяПрава - Строка
//  ИмяМетаданного - Строка
// 
// Возвращаемое значение:
//  Строка
Функция КлючКэшаПоискаПоПравамПользователей(Знач ИмяПрава, ИмяМетаданного)
	
	ИмяПрава = ВРег(ИмяПрава);
	Если ПустаяСтрока(ИмяМетаданного) Тогда
		Возврат ИмяПрава;
	Иначе
		Возврат СтрШаблон("%1 | %2", ВРег(ИмяПрава), ВРег(ИмяМетаданного));
	КонецЕсли;
	
КонецФункции

// Целое число в большую сторону.
// 
// Параметры:
//  Число - Число
// 
// Возвращаемое значение:
//  Число
Функция ЦелВБольшую(Число)
	
	ЧислоЦел = Цел(Число);
	Если Число > ЧислоЦел Тогда
		ЧислоЦел = ЧислоЦел + 1;
	КонецЕсли;
	
	Возврат ЧислоЦел;
	
КонецФункции

#КонецОбласти
