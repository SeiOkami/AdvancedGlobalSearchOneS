// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Значение соответствует шаблону.
// 
// Параметры:
//  Значение - Произвольный
//           - Структура из КлючИЗначение:
//           * Ключ - Строка
//  Шаблон - Структура - Шаблон структуры, наличия свойств которых нужно проверить в значении
// 
// Возвращаемое значение:
//  Булево
Функция ЗначениеСоответствуетШаблону(Значение, Шаблон) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Структура") Тогда
		
		Соответствует = Истина;
		
		Если Шаблон.Количество() <> Значение.Количество() Тогда
			Соответствует = Ложь;
		Иначе
			Для Каждого КлючИЗначение Из Шаблон Цикл
				Если НЕ Значение.Свойство(КлючИЗначение.Ключ) Тогда
					Соответствует = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
	Иначе
		
		Соответствует = Ложь;
		
	КонецЕсли;
	
	Возврат Соответствует;
	
КонецФункции

#Область ВыполняемыеДействия

// Пустое описание оповещения.
// 
// Возвращаемое значение:
//  см. ОписаниеОповещения
Функция ПустоеОписаниеОповещения() Экспорт
	
	Возврат ОписаниеОповещения("", "");
	
КонецФункции

// Описание оповещения
//  
// Параметры: 
//  ИмяПроцедуры - Строка, Неопределено - Имя экспортируемой процедуры модуля, которая будет вызвана.
//  Модуль - ОбщийМодуль, Строка - Программный модуль, процедура которого будет вызвана
//  ДополнительныеПараметры - Неопределено, Произвольный - Значение любого типа, 
//		которое при вызове будет передано в указанную процедуру последним параметром.
//  ИмяПроцедурыОбработкиОшибки - Строка - Имя экспортируемой процедуры модуля, 
//		которая будет вызвана в случае возникновения ошибки.
//  МодульОбработкиОшибки - ОбщийМодуль, Строка, Неопределено - Программный модуль, 
//		процедура которого будет вызвана в случае возникновения ошибки
//		При использовании типа ФормаКлиентскогоПриложения 
//		будет вызван метод модуля указанной формы клиентского приложения. 
//		При использовании типа КомандаКомандногоИнтерфейса 
//		будет вызван метод модуля команды командного интерфейса. 
//		При использовании типа ОбщийМодуль будет вызван метод общего неглобального модуля.
// 
// Возвращаемое значение:
//  Структура - Описание оповещения:
// * ИмяПроцедуры - см. ОписаниеОповещения.ИмяПроцедуры
// * Модуль - см. ОписаниеОповещения.Модуль
// * ДополнительныеПараметры - см. ОписаниеОповещения.ДополнительныеПараметры
// * ИмяПроцедурыОбработкиОшибки - см. ОписаниеОповещения.ИмяПроцедурыОбработкиОшибки
// * МодульОбработкиОшибки - ОбщийМодуль, Строка, Неопределено -
//
// Отключаемые проверки:
//	BSLLS:MissingReturnedValueDescription-off - баг BSL LS
//	BSLLS:MissingParameterDescription-off - баг BSL LS
Функция ОписаниеОповещения(ИмяПроцедуры, Модуль, ДополнительныеПараметры = Неопределено, 
	ИмяПроцедурыОбработкиОшибки = "", МодульОбработкиОшибки = "") Экспорт
	
	ОписаниеОповещения = Новый Структура;
	ОписаниеОповещения.Вставить("ИмяПроцедуры", ИмяПроцедуры);
	ОписаниеОповещения.Вставить("Модуль", Модуль);
	ОписаниеОповещения.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	ОписаниеОповещения.Вставить("ИмяПроцедурыОбработкиОшибки", ИмяПроцедурыОбработкиОшибки);
	ОписаниеОповещения.Вставить("МодульОбработкиОшибки", МодульОбработкиОшибки);
	
	// @skip-check constructor-function-return-section - баг ЕДТ
	Возврат ОписаниеОповещения;
	
КонецФункции

// Параметры для метода см. ВыполняемоеДействиеВводаЗначения
// 
// Параметры:
//  ОписаниеОповещенияОЗавершении - см. ОписаниеОповещения
//  Значение - Произвольный
//  Подсказка - Строка
//  Тип - ОписаниеТипов
// 
// Возвращаемое значение:
//  Структура - :
// * ОписаниеОповещенияОЗавершении - см. ПараметрыВыполняемогоДействияВводаЗначения.ОписаниеОповещенияОЗавершении
// * Значение - см. ПараметрыВыполняемогоДействияВводаЗначения.Значение
// * Подсказка - см. ПараметрыВыполняемогоДействияВводаЗначения.Подсказка
// * Тип - см. ПараметрыВыполняемогоДействияВводаЗначения.Тип
Функция ПараметрыВыполняемогоДействияВводаЗначения(ОписаниеОповещенияОЗавершении, Значение, Подсказка, Тип) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОписаниеОповещенияОЗавершении", ОписаниеОповещенияОЗавершении);
	Результат.Вставить("Значение", Значение);
	Результат.Вставить("Подсказка", Подсказка);
	Результат.Вставить("Тип", Тип);
	
	Возврат Результат;
	
КонецФункции

// Выполняемое действие ввода значения.
// 
// Параметры:
//  ПараметрыДействия - см. ПараметрыВыполняемогоДействияВводаЗначения
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействиеОписанияОповещения
Функция ВыполняемоеДействиеВводаЗначения(ПараметрыДействия) Экспорт
	
	ОписаниеОповещения = ОписаниеОповещения("ОтобразитьВводЗначения", "РГП_ГлобальныйПоискКлиент", ПараметрыДействия);
	Возврат ВыполняемоеДействиеОписанияОповещения(ОписаниеОповещения);
	
КонецФункции

// Выполняемое действие набора выполняемых действий.
// 
// Параметры:
//  Действие1 - см. ВыполняемоеДействие
//  Действие2 - см. ВыполняемоеДействие
//  Действие3 - см. ВыполняемоеДействие
//  
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеНабораДействий(Знач Действие1 = Неопределено, 
	Знач Действие2 = Неопределено, Знач Действие3 = Неопределено) Экспорт
	
	Результат = ВыполняемоеДействие();
	Если Действие1 <> Неопределено Тогда
		Результат.ВыполняемыеДействия.Добавить(Действие1);
	КонецЕсли;
	Если Действие2 <> Неопределено Тогда
		Результат.ВыполняемыеДействия.Добавить(Действие2);
	КонецЕсли;
	Если Действие3 <> Неопределено Тогда
		Результат.ВыполняемыеДействия.Добавить(Действие3);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Выполняемое действие. Может использоваться в команде и в результате поиска
// 
// Возвращаемое значение:
//  Структура:
// * ОткрываемоеЗначение - Неопределено, Произвольный - Значение, которое необходимо открыть
// * КомандаПоиска - Строка - Полный путь команды поиска
// * ПараметрыПоиска - Структура - Параметры команды поиска
// * СтрокаПоиска - Строка - Строка команды поиска
// * ОткрываемаяФорма - см. ОписаниеОткрываемойФормы
// * ОписаниеОповещения - см. ОписаниеОповещения
// * ВыполняемыеДействия - Массив из см. ВыполняемоеДействие
// * ДополнительныеПараметры - Структура из КлючИЗначение - :
// ** Ключ - Строка
// ** Значение - Произвольный
Функция ВыполняемоеДействие() Экспорт

	ВыполняемыеДействия = Новый Массив; // Массив из см. ВыполняемоеДействие
	
	Результат = Новый Структура;
	
	// Выполняемое действие - Команда поиска
	Результат.Вставить("КомандаПоиска", "");
	Результат.Вставить("ПараметрыПоиска", Новый Структура);
	Результат.Вставить("СтрокаПоиска", "");
	
	// Выполняемое действие - Открытие значения
	Результат.Вставить("ОткрываемоеЗначение", Неопределено);	
	
	// Выполняемое действие - Вызов метода
	Результат.Вставить("ОписаниеОповещения", Неопределено);
	
	// Выполняемое действие - Открытие формы
	Результат.Вставить("ОткрываемаяФорма", Неопределено);
	
	// Выполняемое действие - Набор действий
	Результат.Вставить("ВыполняемыеДействия", ВыполняемыеДействия);
	
	// Доп. параметры
	Результат.Вставить("ДополнительныеПараметры", Новый Структура);
	
	// @skip-check constructor-function-return-section - #EDT786
	Возврат Результат;
	
КонецФункции

// Описание открываемой формы.
// 
// Параметры:
//  ИмяФормы - Строка - Имя открываемой формы
//  Параметры - Структура, Неопределено - Параметры формы
// 
// Возвращаемое значение:
//  Структура - Описание открываемой формы:
// * ИмяФормы - см. ОписаниеОткрываемойФормы.ИмяФормы
// * Параметры - см. ОписаниеОткрываемойФормы.Параметры
// * Владелец - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.Владелец
// * Уникальность - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.Уникальность
// * Окно - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.Окно
// * НавигационнаяСсылка - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.НавигационнаяСсылка
// * ОписаниеОповещенияОЗакрытии - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.ОписаниеОповещенияОЗакрытии
// * РежимОткрытияОкна - Неопределено, Произвольный - см. метод платформы ОткрытьФорму.РежимОткрытияОкна
Функция ОписаниеОткрываемойФормы(ИмяФормы, Параметры = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяФормы", ИмяФормы);
	Результат.Вставить("Параметры", Параметры);
	Результат.Вставить("Владелец", Неопределено);
	Результат.Вставить("Уникальность", Неопределено);
	Результат.Вставить("Окно", Неопределено);
	Результат.Вставить("НавигационнаяСсылка", Неопределено);
	Результат.Вставить("ОписаниеОповещенияОЗакрытии", Неопределено);
	Результат.Вставить("РежимОткрытияОкна", Неопределено);

	Возврат Результат;
	
КонецФункции

// Выполняемое действие открытия формы.
// 
// Параметры:
//  ИмяФормы - Строка - Имя открываемой формы
//  ПараметрыФормы - Структура, Неопределено - Параметры формы
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеОткрытияФормы(ИмяФормы, ПараметрыФормы = Неопределено) Экспорт
	
	Результат = ВыполняемоеДействие();
	Результат.ОткрываемаяФорма = ОписаниеОткрываемойФормы(ИмяФормы, ПараметрыФормы);
	Возврат Результат;
	
КонецФункции

// см. ВыполняемоеДействиеОткрытияЗначения
Функция ВыполняемоеДействиеПустое() Экспорт
	
	Возврат Неопределено;
	
КонецФункции

// Выполняемое действие открытия значения.
// 
// Параметры:
//  ОткрываемоеЗначение - Произвольный
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеОткрытияЗначения(ОткрываемоеЗначение) Экспорт
	
	Результат = ВыполняемоеДействие();
	Результат.ОткрываемоеЗначение = ОткрываемоеЗначение;
	
	Возврат Результат;
	
КонецФункции

// Выполняемое действие выполнения описания оповещения
// 
// Параметры:
//  ОписаниеОповещения - см. ОписаниеОповещения
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеОписанияОповещения(ОписаниеОповещения) Экспорт
	
	Результат = ВыполняемоеДействие();
	Результат.ОписаниеОповещения = ОписаниеОповещения;
	
	Возврат Результат;
	
КонецФункции

// Выполняемое действие перехода назад (выход из параметров)
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеНазадИзПараметров(ПараметрыВыполнения) Экспорт
	
	Возврат ВыполняемоеДействиеПереходаНаКоманду(
		ПараметрыВыполнения.ПолныйКлюч, Неопределено, ПараметрыВыполнения.СтрокаПоиска);
	
КонецФункции

// Выполняемое действие обновления результатов поиска
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеОбновитьРезультаты(ПараметрыВыполнения) Экспорт
	
	Возврат ВыполняемоеДействиеПереходаНаКоманду(ПараметрыВыполнения.ПолныйКлюч, 
		ПараметрыВыполнения.ПараметрыКоманды, ПараметрыВыполнения.СтрокаПоиска);
	
КонецФункции

// Выполняемое действие перехода назад (на родительскую команду).
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействиеПереходаНаКоманду
Функция ВыполняемоеДействиеПереходаНазад(ПараметрыВыполнения) Экспорт
	
	Если ПараметрыВыполнения.ПриПереходеНазадОчищатьСтрокуПоиска Тогда
		СтрокаПоиска = "";
	Иначе
		СтрокаПоиска = ПараметрыВыполнения.СтрокаПоиска;
	КонецЕсли;
	
	Возврат ВыполняемоеДействиеПереходаНаКоманду(
		ПараметрыВыполнения.ПолныйКлючРодителя, ПараметрыВыполнения.ПараметрыКоманды, СтрокаПоиска);
	
КонецФункции

// Выполняемое действие перехода на подчиненную команду поиска (с передачей текущих параметров и строки поиска)
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
//  КлючКоманды - Строка - Ключ подчиненной команды
//  ПередаваемыеПараметры - Структура - Дополнительные передаваемые параметры
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеПереходаНаПодчиненнуюКоманду(ПараметрыВыполнения, 
	КлючКоманды = "", ПередаваемыеПараметры = Неопределено) Экспорт
	
	ПолныйКлюч = ПараметрыВыполнения.ПолныйКлюч;
	
	ЧастиПути = Новый Массив; // Массив из Строка
	ЧастиПути.Добавить(ПолныйКлюч);
	Если ЗначениеЗаполнено(КлючКоманды) Тогда
		ЧастиПути.Добавить(КлючКоманды);
		ЧастиПути.Добавить(ПараметрыВыполнения.Разделитель);
	КонецЕсли;
	
	ПолныйПуть = СтрСоединить(ЧастиПути, "");
	
	Возврат ВыполняемоеДействиеПереходаНаКоманду(ПолныйПуть, 
		ПередаваемыеПараметры, ПараметрыВыполнения.СтрокаПоиска);
	
КонецФункции

// Выполняемое действие перехода на команду поиска
// 
// Параметры:
//  ПолныйКлючКоманды - Строка - Полный ключ выполняемой команды поиска
//  ПередаваемыеПараметры - Неопределено, Структура - Параметры выполняения команды
//  СтрокаПоиска - Строка - Строка поиска в команде
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействие
Функция ВыполняемоеДействиеПереходаНаКоманду(ПолныйКлючКоманды = "", 
	ПередаваемыеПараметры = Неопределено, СтрокаПоиска = "") Экспорт
	
	Результат = ВыполняемоеДействие();
	Результат.КомандаПоиска = ПолныйКлючКоманды;
	Результат.СтрокаПоиска = СтрокаПоиска;
	
	Если ТипЗнч(ПередаваемыеПараметры) = Тип("Структура") Тогда
		Результат.ПараметрыПоиска = ПередаваемыеПараметры;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

// Пустые параметры выполнения команды
// Свойства наследуются из см. РГП_ГлобальныйПоискКлиент.НоваяКомандаРасширенногоПоиска
// Заполняется в клиенстком модуле и передается в план поиска
// 
// Возвращаемое значение:
//  Структура - :
// * Ключ - Строка - Ключ команды
// * ПолныйКлюч - Строка - Полный ключ команды
// * ПолныйКлючРодителя - Строка - Полный ключ перехода к команде родителя
// * Разделитель - Строка - Разделитель команд
// * Представление - Строка - Строка, выводимая пользователю
// * ПараметрыКоманды - Структура - Значения параметров команды
// * ПолныйКлючСПараметрами - Строка - Полный ключ команды, включая параметры
// * СтрокаПоиска - Строка - Строка поиска с вырезанным ключем команды
// * СтрокаПоискаВрег - Строка - СтрокаПоиска в верхнем регистре
// * СловаПоиска - Массив из Строка - СтрокаПоискаВрег разбитая на слова
// * МаксимальныйПриоритет - Число - Максимальный приоритет, устанавливаемый для идеального совпадения по строке поиска
// * ЛимитСтрокРезультата - Число - Максимальное число выводимых строк результата
// * СтандартныеКоманды - Массив из Строка - Массив стандартных вспомогательных команд в начале списка результата, см. СтандартныеКоманды
// * ПользовательскиеНастройки - см. НовыеПользовательскиеНастройки
// * ПриПереходеНазадОчищатьСтрокуПоиска - Булево - Стоит ли при переходе "назад" очищать строку поиска
// * ДополнительныеПараметры - Структура из КлючИЗначение:
// ** Ключ - Строка
// ** Значение - Произвольный
Функция ШаблонПараметровВыполненияКомандыПоиска() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ключ", "");
	Результат.Вставить("ПолныйКлюч", "");
	Результат.Вставить("ПолныйКлючРодителя", "");
	Результат.Вставить("Разделитель", "");
	Результат.Вставить("Представление", "");
	Результат.Вставить("ПараметрыКоманды", Новый Структура);
	Результат.Вставить("ПолныйКлючСПараметрами", "");
	Результат.Вставить("СтрокаПоиска", "");
	Результат.Вставить("СтрокаПоискаВрег", "");
	Результат.Вставить("СловаПоиска", Новый Массив);
	Результат.Вставить("МаксимальныйПриоритет", 999);
	Результат.Вставить("ЛимитСтрокРезультата", 0);
	Результат.Вставить("СтандартныеКоманды", Новый Массив);
	Результат.Вставить("ПользовательскиеНастройки", НовыеПользовательскиеНастройки());
	Результат.Вставить("ПриПереходеНазадОчищатьСтрокуПоиска", Ложь);
	Результат.Вставить("ДополнительныеПараметры", Новый Структура);
	
	Возврат Результат;
	
КонецФункции

// Ключ вида расширенного глобального поиска. 
// По нему определяем, что при нажатии нужно выполнять свою логику
// 
// Возвращаемое значение:
//  Строка
Функция КлючВидаРасширенногоГлобальногоПоиска() Экспорт
	
	Возврат КлючПодсистемы();
	
КонецФункции

// Ключ подсистемы.
// 
// Возвращаемое значение:
//  Строка
Функция КлючПодсистемы() Экспорт
	
	Возврат "РГП_РасширенныйГлобальныйПоиск";
	
КонецФункции

// Необходимо ли переданный ЭлементРезультата обрабатывать методами расширения
// 
// Параметры:
//  ЭлементРезультата - ЭлементРезультатаГлобальногоПоиска
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоЭлементРезультатаРасширенногоГлобальногоПоиска(ЭлементРезультата) Экспорт
	
	Возврат ЭлементРезультата.ВидПоиска = КлючВидаРасширенногоГлобальногоПоиска();
	
КонецФункции

// Добавляет элемент результата глобального поиска.
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска
//  ВыполняемоеДействие - Произвольный
//                      - см. ВыполняемоеДействие
//  Представление - Строка, ФорматированнаяСтрока - Представление
//  Картинка - Неопределено, Картинка - Картинка
//  Описание - Строка - Описание
Процедура ДобавитьЭлементРезультатаГлобальногоПоиска(РезультатыПоиска, 
	ВыполняемоеДействие, Представление = "", 
	Картинка = Неопределено, Описание = "") Экспорт
	
	ЭлементРезультата = ЭлементРезультатаГлобальногоПоиска(
		ВыполняемоеДействие, Представление, Картинка, Описание);
	
	РезультатыПоиска.Добавить(ЭлементРезультата);
	
КонецПроцедуры

// Новый элемент результата глобального поиска.
// 
// Параметры:
//  ВыполняемоеДействие - Произвольный
//                      - см. ВыполняемоеДействие
//  Представление - Строка - Представление
//  Картинка - Неопределено, Картинка - Картинка
//  Описание - Строка - Описание
// 
// Возвращаемое значение:
//  ЭлементРезультатаГлобальногоПоиска
Функция ЭлементРезультатаГлобальногоПоиска(ВыполняемоеДействие, 
	Представление = "", Картинка = Неопределено, Описание = "") Экспорт
	
	Возврат Новый ЭлементРезультатаГлобальногоПоиска(ВыполняемоеДействие, 
		Представление, Картинка, КлючВидаРасширенногоГлобальногоПоиска(), Описание);
	
КонецФункции

// Добавить элемент результата стандартной команды
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
//  ЭлементРезультата - ЭлементРезультатаГлобальногоПоиска
Процедура ДобавитьЭлементРезультатаСтандартнойКоманды(РезультатыПоиска, 
	ПараметрыВыполнения, ЭлементРезультата) Экспорт
	
	Если ПараметрыВыполнения.ПользовательскиеНастройки.ГруппироватьСтандартныеКоманды Тогда
		ОбщийЭлемент = РезультатыПоиска[РезультатыПоиска.Количество() - 1];
		ДобавитьДействиеНаОсновеЭлементаРезультата(ОбщийЭлемент, ЭлементРезультата);
	Иначе
		РезультатыПоиска.Добавить(ЭлементРезультата);
	КонецЕсли;
	
КонецПроцедуры

// Добавить действие на основе элемента результата.
// 
// Параметры:
//  ПолучательДействия - ЭлементРезультатаГлобальногоПоиска - Элемент результата, в который необходимо добавить действие
//  ЭлементРезультата - ЭлементРезультатаГлобальногоПоиска - Элемент результата, которое нужно перенести в действие
Процедура ДобавитьДействиеНаОсновеЭлементаРезультата(ПолучательДействия, ЭлементРезультата) Экспорт
	
	ПолучательДействия.Действия.Добавить(ЭлементРезультата.Значение, 
		ЭлементРезультата.Представление, ЭлементРезультата.Картинка);
	
КонецПроцедуры

// Добавить элемент результата стандартной команды ввода строки
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
Процедура ДобавитьЭлементРезультатаСтандартнойКоманды_ВводСтроки(РезультатыПоиска, ПараметрыВыполнения) Экспорт
	
	Элемент = ЭлементРезультатаКомандаВводаСтроки(ПараметрыВыполнения);
	
	ДобавитьЭлементРезультатаСтандартнойКоманды(РезультатыПоиска, ПараметрыВыполнения, Элемент);
	
КонецПроцедуры

// Добавить элемент результата стандартной команды обновления результатов поиска
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
Процедура ДобавитьЭлементРезультатаСтандартнойКоманды_Обновить(РезультатыПоиска, ПараметрыВыполнения) Экспорт
	
	ВыполняемоеДействие = ВыполняемоеДействиеОбновитьРезультаты(ПараметрыВыполнения);
	ЭлементРезультата = ЭлементРезультатаГлобальногоПоиска(
		ВыполняемоеДействие, СтрокаЖирным("Обновить"), БиблиотекаКартинок.Обновить);
	
	ДобавитьЭлементРезультатаСтандартнойКоманды(РезультатыПоиска, ПараметрыВыполнения, ЭлементРезультата);
	
КонецПроцедуры

// Добавить элемент результата стандартной команды возвращения на команду вверх
// 
// Параметры:
//  РезультатыПоиска - РезультатГлобальногоПоиска
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
Процедура ДобавитьЭлементРезультатаСтандартнойКоманды_Назад(РезультатыПоиска, ПараметрыВыполнения) Экспорт
	
	ВыполняемоеДействие = ВыполняемоеДействиеПереходаНазад(ПараметрыВыполнения);
	
	ЭлементРезультата = ЭлементРезультатаГлобальногоПоиска(
		ВыполняемоеДействие, СтрокаЖирным("Назад"), БиблиотекаКартинок.Назад);
		
	ДобавитьЭлементРезультатаСтандартнойКоманды(
		РезультатыПоиска, ПараметрыВыполнения, ЭлементРезультата);
	
КонецПроцедуры

// Элемент результата команда ввода строки в конце поля поиска.
// 
// Параметры:
//  ПараметрыВыполнения - см. ШаблонПараметровВыполненияКомандыПоиска
// 
// Возвращаемое значение:
//  ЭлементРезультатаГлобальногоПоиска
Функция ЭлементРезультатаКомандаВводаСтроки(ПараметрыВыполнения) Экспорт
	
	СтрокаПоиска = ПараметрыВыполнения.ПолныйКлючСПараметрами;
	НачальнаяСтрока = ПараметрыВыполнения.СтрокаПоиска;
	Представление = "Строка поиска";
	
	Если ЗначениеЗаполнено(НачальнаяСтрока) 
		И НЕ ПараметрыВыполнения.ПользовательскиеНастройки.ГруппироватьСтандартныеКоманды Тогда
		Представление = СтрШаблон("%1: %2", Представление, НачальнаяСтрока);
	КонецЕсли;
	
	ПараметрыМетода = ПараметрыДействияВводаСтроки(СтрокаПоиска, НачальнаяСтрока, Представление);
	
	Представление = СтрокаЖирным(Представление);
	
	ОписаниеОповещения = ОписаниеОповещения(
		"ОтобразитьВводСтроки", "РГП_ГлобальныйПоискКлиент", ПараметрыМетода);
	ВыполняемоеДействие = ВыполняемоеДействиеОписанияОповещения(ОписаниеОповещения);
	
	ЭлементРезультата = ЭлементРезультатаГлобальногоПоиска(
		ВыполняемоеДействие, Представление, БиблиотекаКартинок.ПоискДанных);
		
	Если ЗначениеЗаполнено(НачальнаяСтрока) 
		И НЕ ПараметрыВыполнения.ПользовательскиеНастройки.ГруппироватьСтандартныеКоманды Тогда
		
		ДействиеОчистить = ВыполняемоеДействиеПереходаНаКоманду(
			ПараметрыВыполнения.ПолныйКлюч, ПараметрыВыполнения.ПараметрыКоманды);
		ЭлементРезультата.Действия.Добавить(ДействиеОчистить, "Очистить", БиблиотекаКартинок.Очистить);
		
	КонецЕсли;
	
	Возврат ЭлементРезультата;
	
КонецФункции

// Параметры действия ввода строки.
// 
// Параметры:
//  СтрокаПоиска - Строка - Строка поиска, в конце которой будет подставлена введенная строка
//  НачальнаяСтрока - Строка - Начальное значение строки, которое будет использовано в качестве начального значения в окне ввода.
//  Представление - Строка - Представление в заголовке поля ввода
// 
// Возвращаемое значение:
//  Структура - Параметры действия ввода строки:
//  * СтрокаПоиска - см. ПараметрыДействияВводаСтроки.СтрокаПоиска
//  * НачальнаяСтрока - см. ПараметрыДействияВводаСтроки.НачальнаяСтрока
//  * Представление - см. ПараметрыДействияВводаСтроки.Представление
Функция ПараметрыДействияВводаСтроки(СтрокаПоиска, НачальнаяСтрока, Представление) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("СтрокаПоиска", СтрокаПоиска);
	Параметры.Вставить("Представление", Представление);
	Параметры.Вставить("НачальнаяСтрока", НачальнаяСтрока);
	
	Возврат Параметры;
	
КонецФункции

// Стандартные команды
// 
// Возвращаемое значение:
//  Структура - Стандартные команды:
// * Назад - Строка - Команда "Назад" для возвращения к предыдущей команде
// * ВводСтроки - Строка - Команда "Ввод строки" для изменения строки поиска
// * Обновить - Строка - Команда "Обновить" для обновления результатов поиска
// * Параметры - Строка - Команда "Параметры" отображается, когда строка поиска содержит параметры
Функция СтандартныеКоманды() Экспорт

	Команды = Новый Структура;
	Команды.Вставить("Назад", "Назад");
	Команды.Вставить("ВводСтроки", "ВводСтроки");
	Команды.Вставить("Обновить", "Обновить");
	Команды.Вставить("Параметры", "Параметры");
	
	Возврат Команды;
	
КонецФункции

// Выполняемое действие вычисления выражения.
// 
// Параметры:
//  ПолныйКлючКоманды - Строка
//  НаСервере - Булево
//  НачальноеВыражение - Строка
// 
// Возвращаемое значение:
//  см. ВыполняемоеДействиеОписанияОповещения
Функция ВыполняемоеДействиеВычисленияВыражения(ПолныйКлючКоманды, НаСервере, НачальноеВыражение = "") Экспорт
	
	ИмяМодуля = "РГП_ГлобальныйПоискКлиент";
	ИмяМетода = "ГлобальныйПоискВычислитьВыражение";
	
	ПараметрыОповещения = ДополнительныеПараметрыВыполненияКомандыВычисленияВыражения(
		НаСервере, НачальноеВыражение, ПолныйКлючКоманды);
		
	ОписаниеОповещения  = ОписаниеОповещения(ИмяМетода, ИмяМодуля, ПараметрыОповещения);
	
	Возврат ВыполняемоеДействиеОписанияОповещения(ОписаниеОповещения);
	
КонецФункции

// Дополнительные параметры выполнения команды вычисления выражения.
// 
// Параметры:
//  НаСервере - Булево
//  НачальноеВыражение - Строка
//  ПолныйКлючКоманды - Строка
// 
// Возвращаемое значение:
//  Структура:
// * НаСервере - Булево
// * НачальноеВыражение - Строка
// * ПолныйКлючКоманды - Строка
Функция ДополнительныеПараметрыВыполненияКомандыВычисленияВыражения(
	НаСервере, НачальноеВыражение, ПолныйКлючКоманды) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НаСервере", НаСервере);
	Результат.Вставить("НачальноеВыражение", НачальноеВыражение);
	Результат.Вставить("ПолныйКлючКоманды" , ПолныйКлючКоманды);
	
	Возврат Результат;
	
КонецФункции

// Элемент истории вычисления выражения.
// 
// Возвращаемое значение:
//  Структура -  Элемент истории вычисления выражения:
// * НаСервере - Булево - Признак вычисления на сервере
// * Выражение - Строка - Текст выражения
// * ОписаниеОшибки - Строка
// * Успешно - Булево - Успешно ли выполнилось выражение или упало в ошибку
// * ПредставлениеТипа - Строка - Тип результата строкой
// * Результат - Неопределено, Произвольный - Результат выражения
// * Представление - Строка
// * Дата - Дата - Дата последнего использования
Функция ЭлементИсторииВычисленияВыражения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НаСервере", Ложь);
	Результат.Вставить("Выражение", "");
	Результат.Вставить("Успешно", Ложь);
	Результат.Вставить("ОписаниеОшибки", "");
	Результат.Вставить("ПредставлениеТипа", "");
	Результат.Вставить("Результат", Неопределено);
	Результат.Вставить("Представление", "");
	Результат.Вставить("Дата", '0001 01 01');
	
	Возврат Результат;
	
КонецФункции

#Область ПользовательскиеНастройки

// Новые пользовательские настройки.
// 
// Возвращаемое значение:
//  Структура - :
// * ОбщиеНастройки - см. НовыеПользовательскиеНастройки
Функция НоваяКоллекцияПользовательскихНастроек() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОбщиеНастройки", НовыеПользовательскиеНастройки());
	
	Возврат Результат;
	
КонецФункции

// Новые пользовательские настройки.
// 
// Возвращаемое значение:
//  Структура - :
// * ГруппироватьСтандартныеКоманды - Булево - Требуется ли группировать стандартные команды в одном пункте меню
Функция НовыеПользовательскиеНастройки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ГруппироватьСтандартныеКоманды", Истина);
	
	Возврат Результат;
	
КонецФункции

// Описания доступных пользовательских настроек.
// 
// Возвращаемое значение:
//  Структура из КлючИЗначение:
//  * Ключ - Строка
//  * Значение - см. НовыйОписаниеПользовательскойНастройки
Функция ОписанияДоступныхПользовательскихНастроек() Экспорт
	
	Результат = Новый Структура;
	
	ТекущаяНастройка = НовыйОписаниеПользовательскойНастройки();
	ТекущаяНастройка.Ключ = "ГруппироватьСтандартныеКоманды";
	ТекущаяНастройка.Представление = "Группировать стандартные команды";
	ТекущаяНастройка.Пояснение = "Стоит ли группировать стандартные команды в одном элементе";
	ТекущаяНастройка.Тип = Новый ОписаниеТипов("Булево");
	ТекущаяНастройка.ДоступноАвто = Истина;
	ТекущаяНастройка.ФорматнаяСтрока = "БЛ='Не группировать'; БИ=Группировать;";
	
	Результат.Вставить(ТекущаяНастройка.Ключ, ТекущаяНастройка);
	
	Возврат Результат;
	
КонецФункции

// Описание пользовательской настройки.
// 
// Параметры:
//  ОписанияНастроек - см. ОписанияДоступныхПользовательскихНастроек
//  КлючНастройки - Строка
//  ВызыватьИсключение - Булево
// 
// Возвращаемое значение:
//  см. НовыйОписаниеПользовательскойНастройки
Функция ОписаниеПользовательскойНастройки(ОписанияНастроек, КлючНастройки, ВызыватьИсключение = Истина) Экспорт
	
	ОписаниеНастройки = Неопределено; // см. НовыйОписаниеПользовательскойНастройки
	ОписанияНастроек.Свойство(КлючНастройки, ОписаниеНастройки);
	Если ОписаниеНастройки = Неопределено И ВызыватьИсключение Тогда
		ВызватьИсключение "Не найдено описание пользовательской настройки: " + КлючНастройки;
	Иначе
		//@skip-check constructor-function-return-section - баг ЕДТ
		Возврат ОписаниеНастройки;
	КонецЕсли;
	
КонецФункции

// Приводит переданное значение в соответствии с описанием пользовательской настройки.
// 
// Параметры:
//  Значение - Произвольный - Проверяемое значение
//  ОписаниеНастройки - см. НовыйОписаниеПользовательскойНастройки
//  ЗначениеИзменено - Булево - Было ли изменено значение
// 
Процедура ПривестиЗначениеКПользовательскойНастройке(Значение, Знач ОписаниеНастройки, ЗначениеИзменено = Ложь) Экспорт
	
	Если ОписаниеНастройки.ДоступноАвто И Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовоеЗначение = ОписаниеНастройки.Тип.ПривестиЗначение(Значение);
	Если НЕ ЗначениеПользовательскойНастройкиЕстьВДоступных(ОписаниеНастройки, НовоеЗначение) Тогда
		НовоеЗначение = ОписаниеНастройки.ЗначениеПоУмолчанию;
	КонецЕсли;
	
	ЗначениеИзменено = НовоеЗначение <> Значение;
	Если ЗначениеИзменено Тогда
		Значение = НовоеЗначение;
	КонецЕсли;
	
КонецПроцедуры

// Значение пользовательской настройки есть в доступных.
// 
// Параметры:
//  ОписаниеНастройки - см. НовыйОписаниеПользовательскойНастройки
//  Значение - Произвольный
// 
// Возвращаемое значение:
//  Булево
Функция ЗначениеПользовательскойНастройкиЕстьВДоступных(ОписаниеНастройки, Значение) Экспорт
	
	Если ЗначениеЗаполнено(ОписаниеНастройки.ДоступныеЗначения) Тогда
		
		ЗначениеДоступно = ОписаниеНастройки.ДоступныеЗначения.НайтиПоЗначению(Значение) <> Неопределено;
		
	ИначеЕсли Значение = Неопределено Тогда
		
		ЗначениеДоступно = ОписаниеНастройки.ДоступноАвто;
		
	Иначе
		
		ЗначениеДоступно = Истина;
		
	КонецЕсли;
	
	Возврат ЗначениеДоступно;
	
КонецФункции

// Представление значения пользовательской настройки.
// 
// Параметры:
//  ОписаниеНастройки - см. ОписаниеПользовательскойНастройки
//  Значение - Булево, Число, Дата, Строка, Произвольный - Значение настройки
// 
// Возвращаемое значение:
//   Строка
Функция ПредставлениеЗначенияПользовательскойНастройки(ОписаниеНастройки, Значение) Экспорт
	
	Если ОписаниеНастройки.ДоступноАвто И Значение = Неопределено Тогда
		
		Представление = ПредставлениеЗначенияАвтоПользовательскойНастройки();
		
	Иначе
		
		Представление = Формат(Значение, ОписаниеНастройки.ФорматнаяСтрока);
		
		Если ЗначениеЗаполнено(ОписаниеНастройки.ДоступныеЗначения) Тогда
			ДоступноеЗначение  = ОписаниеНастройки.ДоступныеЗначения.НайтиПоЗначению(Значение);
			Если ДоступноеЗначение <> Неопределено Тогда
				Представление = ДоступноеЗначение.Представление;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Представление;

КонецФункции

// Новый описание пользовательской настройки.
// 
// Возвращаемое значение:
//  Структура - :
// * Ключ - Строка - Ключ настройки
// * Представление - Строка -  Представление для пользователя
// * Пояснение - Строка -  Расширенное представление для интерфейса настройки
// * Картинка - Картинка
// * ЗначениеПоУмолчанию - Неопределено, Произвольный - Значение по умолчанию
// * Тип - ОписаниеТипов - Тип значения
// * ДоступныеЗначения - СписокЗначений из Произвольный - Доступные значения
// * ФорматнаяСтрока - Строка - Форматная строка представления значения
// * ДоступноАвто - Булево - Доступность значения "Авто" (Неопределено)
Функция НовыйОписаниеПользовательскойНастройки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ключ", "");
	Результат.Вставить("Представление", "");
	Результат.Вставить("Пояснение", "");
	Результат.Вставить("Картинка", БиблиотекаКартинок.Параметры);
	Результат.Вставить("ЗначениеПоУмолчанию", Неопределено);
	Результат.Вставить("Тип", Новый ОписаниеТипов);
	Результат.Вставить("ДоступныеЗначения", Новый СписокЗначений());
	Результат.Вставить("ФорматнаяСтрока", "");
	Результат.Вставить("ДоступноАвто", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Представление значения авто пользовательской настройки.
// 
// Возвращаемое значение:
//  Строка
Функция ПредставлениеЗначенияАвтоПользовательскойНастройки() Экспорт
	
	Возврат "Авто";
	
КонецФункции

#КонецОбласти

#Область ПараметрыКоманд

// Добавить доступный параметр команды.
// 
// Параметры:
//  Коллекция - см. НоваяКоллекцияПараметровКоманды
//  Параметр - см. ОписаниеПараметраКоманды
// 
Процедура ДобавитьДоступныйПараметрКоманды(Коллекция, Параметр) Экспорт
	
	Коллекция.Вставить(Параметр.Ключ, Параметр);
	
КонецПроцедуры

// Коллекция параметров команды.
// 
// Возвращаемое значение:
//  Структура из КлючИЗначение :
// * Ключ - Строка - Ключ параметра
// * Значение - см. ОписаниеПараметраКоманды
Функция НоваяКоллекцияПараметровКоманды() Экспорт
	
	Возврат Новый Структура;
	
КонецФункции

// Описание параметра команды.
// 
// Параметры:
//  Ключ - Строка - Ключ параметра
//  Представление - Строка - Представление параметра
//  Тип - см. ОписаниеТипов.Тип
//   
// Возвращаемое значение:
//  Структура:
// * Ключ - Строка - Ключ
// * Представление - Строка - Представление
// * Тип - ОписаниеТипов - Ограничение типа 
Функция ОписаниеПараметраКоманды(Ключ = "", Представление = "", Тип = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ключ", Ключ);
	Результат.Вставить("Представление", Представление);
	Результат.Вставить("Тип", ОписаниеТипов(Тип));
	
	Возврат Результат;
	
КонецФункции

// Значение параметра команды страница.
// 
// Параметры:
//  КоллекцияЗначений - Структура
// 
// Возвращаемое значение:
//  Число, Неопределено - Текущая страница
Функция ЗначениеПараметраКоманды_Страница(КоллекцияЗначений) Экспорт
	
	КлючПараметра = КлючПараметраКоманды_Страница();
	ЗначениеПараметра = Неопределено;
	КоллекцияЗначений.Свойство(КлючПараметра, ЗначениеПараметра);
	
	Возврат ЗначениеПараметра;
	
КонецФункции

// Описание параметра команды - Текущая страница.
// 
// Возвращаемое значение:
//  см. ОписаниеПараметраКоманды
Функция ОписаниеПараметраКоманды_Страница() Экспорт
	
	КлючПараметра = КлючПараметраКоманды_Страница();
	Возврат ОписаниеПараметраКоманды(КлючПараметра, "Страница", "Число");
	
КонецФункции

// Ключ параметра команды страница.
// 
// Возвращаемое значение:
//  Строка
Функция КлючПараметраКоманды_Страница() Экспорт
	
	Возврат "С";
	
КонецФункции

#КонецОбласти

#Область Вспомогательные

// Проверяет доступность хотя бы одной роли с учетом данных в кэше
// Сначала проверяем все роли из кэша. Если ролей нет в кэше, то запрашиваем их, помещаем в кэш и проверяем снова
// 
// Параметры:
//  Параметры - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
//  ИменаРолей - см. МассивИменРолейИзПараметра.ИменаРолей
// 
// Возвращаемое значение:
//  Булево
Функция РолиДоступны(Параметры, ИменаРолей) Экспорт
	
	Если ЭтоПолноправныйПользователь(Параметры) Тогда
		Возврат Истина;
	КонецЕсли;
	
	РолиПользователя = Параметры.КэшированныеДанные.РолиПользователя;
	ПолучаемыеРоли = Новый Массив; // Массив из Строка
	
	КоллекцииРолей = Новый Массив; // Массив из Массив
	КоллекцииРолей.Добавить(МассивИменРолейИзПараметра(ИменаРолей));
	КоллекцииРолей.Добавить(ПолучаемыеРоли);
	
	Для НомерКоллекции = 1 По КоллекцииРолей.Количество() Цикл
		
		ЭтоПерваяКоллекция = (НомерКоллекции = 1);
		КоллекцияРолей     = КоллекцииРолей.Получить(НомерКоллекции - 1);
		
		Для Каждого ИмяРоли Из КоллекцияРолей Цикл // Строка
			
			РольДоступна = РолиПользователя.Получить(ИмяРоли);
			Если РольДоступна = Истина Тогда
				Возврат Истина;
			ИначеЕсли РольДоступна = Неопределено И ЭтоПерваяКоллекция Тогда
				ПолучаемыеРоли.Добавить(ИмяРоли);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЭтоПерваяКоллекция Тогда
			ДополнитьКэшРолейПользователей(Параметры, ПолучаемыеРоли);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет доступность права пользователя
// Сначала проверяем все роли из кэша. Если ролей нет в кэше, то запрашиваем их, помещаем в кэш и проверяем снова
// 
// Параметры:
//  Параметры - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
//  ИмяПрава - Строка
//  ИмяМетаданного - Строка
// 
// Возвращаемое значение:
//  Булево
Функция ЕстьПравоДоступа(Параметры, ИмяПрава, ИмяМетаданного = "") Экспорт
	
	Если ЭтоПолноправныйПользователь(Параметры) Тогда
		Возврат Истина;
	КонецЕсли;
	
	ПраваПользователя = Параметры.КэшированныеДанные.ПраваПользователя;
	КлючПоискаПрава   = КлючКэшаПоискаПоПравамПользователей(ИмяПрава, ИмяМетаданного);
	ЕстьПравоДоступа  = ПраваПользователя.Получить(КлючПоискаПрава);
	Если ЕстьПравоДоступа = Неопределено Тогда
		ДополнитьКэшПравПользователей(Параметры, ИмяПрава, ИмяМетаданного);
		ЕстьПравоДоступа = ПраваПользователя.Получить(КлючПоискаПрава);
	КонецЕсли;
	
	Возврат ЕстьПравоДоступа;
	
КонецФункции

// Дополняет кэш доступности ролей пользователей
// 
// Параметры:
//  Параметры - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
//  ИменаРолей - см. МассивИменРолейИзПараметра.ИменаРолей
// 
Процедура ДополнитьКэшРолейПользователей(Параметры, ИменаРолей) Экспорт
	
	МассивРолей = МассивИменРолейИзПараметра(ИменаРолей);
	// @skip-check unknown-method-property - Баг ЕДТ
	ДополнениеКэша = РГП_ГлобальныйПоискВызовСервера.ДоступностьРолейПользователя(МассивРолей);
	Для Каждого КлючИЗначение Из ДополнениеКэша Цикл
		Параметры.КэшированныеДанные.РолиПользователя.Вставить(ВРег(КлючИЗначение.Ключ), КлючИЗначение.Значение);
	КонецЦикла;
	
КонецПроцедуры

// Дополняет кэш доступности прав пользователей
// 
// Параметры:
//  Параметры - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
//  ИмяПрава - Строка
//  ИмяМетаданного - Строка
// 
Процедура ДополнитьКэшПравПользователей(Параметры, ИмяПрава, ИмяМетаданного = "") Экспорт
	
	// @skip-check unknown-method-property - Баг ЕДТ
	ЕстьПравоДоступа  = РГП_ГлобальныйПоискВызовСервера.ЕстьПравоДоступа(ИмяПрава, ИмяМетаданного);
	КлючПоискаПрава   = КлючКэшаПоискаПоПравамПользователей(ИмяПрава, ИмяМетаданного);
	Параметры.КэшированныеДанные.ПраваПользователя.Вставить(КлючПоискаПрава, ЕстьПравоДоступа);
	
КонецПроцедуры

// Массив имен ролей из параметра.
// 
// Параметры:
//  ИменаРолей - Строка, Массив из Строка - Массив имен или строка, разделенная запятыми или переносами строк
// 
// Возвращаемое значение:
//  Массив из Строка
Функция МассивИменРолейИзПараметра(ИменаРолей) Экспорт
	
	Если ТипЗнч(ИменаРолей) = Тип("Строка") Тогда
		Результат = СтрРазделить(ИменаРолей, "," + Символы.ПС, Ложь);
	ИначеЕсли ТипЗнч(ИменаРолей) = Тип("Массив") Тогда
		Результат = ИменаРолей;
	Иначе
		ВызватьИсключение СтрШаблон("Передан параметр некорректного типа %1", ТипЗнч(ИменаРолей));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Это полноправный пользователь.
// 
// Параметры:
//  Параметры - см. РГП_ГлобальныйПоискКлиент.ПараметрыРасширенногоГлобальногоПоиска
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоПолноправныйПользователь(Параметры) Экспорт
	
	Возврат Параметры.КэшированныеДанные.ЭтоПолноправныйПользователь;
	
КонецФункции

// Вычислить выражение.
// 
// Параметры:
//  ТекстВыражения - Строка
//  НаСервере - Булево
// 
// Возвращаемое значение:
//  см. РГП_ГлобальныйПоискКлиентСервер.ЭлементИсторииВычисленияВыражения
Функция ВычислитьВыражение(ТекстВыражения, НаСервере = Ложь) Экспорт
	
	Результат = ЭлементИсторииВычисленияВыражения();
	
	Если НаСервере Тогда
		
		// @skip-check unknown-method-property - Баг ЕДТ
		РезультатСервер = РГП_ГлобальныйПоискВызовСервера.ВычислитьВыражение(ТекстВыражения);
		ЗаполнитьЗначенияСвойств(Результат, РезультатСервер);
		Результат.НаСервере = Истина;
		
	Иначе
		
		Результат.Выражение = ТекстВыражения;
		Результат.Дата = РГП_ГлобальныйПоискВызовСервера.ТекущаяДатаСервера();
	
		// @skip-check server-execution-safe-mode - в данном случае весь смысл выполнить выражение без ограничений
		Попытка
			Значение = Вычислить(ТекстВыражения); // BSLLS:ExecuteExternalCodeInCommonModule-off
			Результат.Представление = Строка(Значение);
			Результат.ПредставлениеТипа = Строка(ТипЗнч(Значение));
			#Если Сервер ИЛИ ТолстыйКлиентУправляемоеПриложение Тогда
				Попытка
					Результат.Результат = ПоместитьВоВременноеХранилище(Значение);
				Исключение
					Результат.Результат = Результат.Представление;
				КонецПопытки;
			#Иначе
				Результат.Результат = Значение;
			#КонецЕсли
			Результат.Успешно = Истина;
		Исключение
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Если ТипЗнч(ИнформацияОбОшибке.Причина) = Тип("ИнформацияОбОшибке") Тогда
				ИнформацияОбОшибке = ИнформацияОбОшибке.Причина;
			КонецЕсли;
			Результат.ОписаниеОшибки = ИнформацияОбОшибке.Описание;
			ЛишнееНачало = "{<Неизвестный модуль>(1,1)}: ";
			Если СтрНачинаетсяС(Результат.ОписаниеОшибки, ЛишнееНачало) Тогда
				Результат.ОписаниеОшибки = Сред(Результат.ОписаниеОшибки, СтрДлина(ЛишнееНачало) + 1);
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Обрезать строку.
// 
// Параметры:
//  Строка - Строка - Строка, которую нужно обрезать, если ее длина превышает допустимую
//  МаксимальнаяДлина - Число - Максимально допустимая длина строки
Процедура ОбрезатьСтроку(Строка, Знач МаксимальнаяДлина) Экспорт
	
	Если СтрДлина(Строка) > МаксимальнаяДлина Тогда
		ОкончаниеСтроки = "...";
		Строка = Лев(Строка, МаксимальнаяДлина - СтрДлина(ОкончаниеСтроки)) + ОкончаниеСтроки;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет строку на соответствие формату имени метода (вызывает исключение).
// 
// Параметры:
//  ИмяМетода - Строка
Процедура ПроверитьИмяМетода(ИмяМетода) Экспорт
	
	ЭтоИмяМетода(ИмяМетода);
	
КонецПроцедуры

// Проверяет строку на соответствие формату имени метода.
// 
// Параметры:
//  ИмяМетода - Строка
//  ВызыватьИсключение - Булево
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоИмяМетода(ИмяМетода, ВызыватьИсключение = Истина) Экспорт
	
	Попытка
		ВременнаяСтруктура = Новый Структура;
		ВременнаяСтруктура.Вставить(ИмяМетода);
		Возврат Истина;
	Исключение
		Если ВызыватьИсключение Тогда
			ВызватьИсключение "Значение не соответствует имени метода: " + ИмяМетода;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецПопытки;
	
КонецФункции

// Строка жирным шрифтом.
// 
// Параметры:
//  Строка - Строка
// 
// Возвращаемое значение:
//  ФорматированнаяСтрока
Функция СтрокаЖирным(Строка) Экспорт
	
	Возврат Новый ФорматированнаяСтрока(Строка, ЖирныйШрифт());
	
КонецФункции

// Жирный шрифт.
// 
// Возвращаемое значение:
//  Шрифт
Функция ЖирныйШрифт() Экспорт
	
	//@skip-check new-font - шрифты стиля недоступны на клиенте
	Возврат Новый Шрифт(, , Истина); // BSLLS:StyleElementConstructors-off
	
КонецФункции

// Заполнить структуру рекурсивно.
// 
// Параметры:
//  Приемник - Структура
//  Источник - Структура
//  ИгнорироватьНеопределено - Булево - Игнорировать ли значение Неопределено из Источника
Процедура ЗаполнитьСтруктуруРекурсивно(Приемник, Источник, ИгнорироватьНеопределено = Ложь) Экспорт
	
	ТипСтруктура = Тип("Структура");
	Если ТипЗнч(Приемник) <> ТипСтруктура 
		ИЛИ ТипЗнч(Источник) <> ТипСтруктура Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из Приемник Цикл
		
		ТекущийКлюч = КлючИЗначение.Ключ;
		ЗначениеПриемника = КлючИЗначение.Значение;
		ЗначениеИсточника = Неопределено; // Структура, Произвольный
		Если Источник.Свойство(ТекущийКлюч, ЗначениеИсточника) Тогда
			Если ТипЗнч(ЗначениеПриемника) = Тип("Структура") Тогда
				ЗаполнитьСтруктуруРекурсивно(ЗначениеПриемника, ЗначениеИсточника, ИгнорироватьНеопределено);
			ИначеЕсли ЗначениеИсточника = Неопределено И ИгнорироватьНеопределено Тогда
				Продолжить;
			Иначе
				Приемник[ТекущийКлюч] = ЗначениеИсточника;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Это простой тип (строка, число, дата, булево).
// 
// Параметры:
//  Тип - Тип
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоПростойТип(Тип) Экспорт
	
	Возврат Тип = Тип("Строка") 
		ИЛИ Тип = Тип("Число")
		ИЛИ Тип = Тип("Дата")
		ИЛИ Тип = Тип("Булево"); 
	
КонецФункции

// Описание типов.
// 
// Параметры:
//  Тип - Строка, Тип, ОписаниеТипов, Неопределено - Тип(ы)
// 
// Возвращаемое значение:
//  ОписаниеТипов
Функция ОписаниеТипов(Тип) Экспорт
	
	Если Тип = Неопределено Тогда
		Возврат Новый ОписаниеТипов;
	ИначеЕсли ТипЗнч(Тип) = Тип("ОписаниеТипов") Тогда
		Возврат Тип;
	ИначеЕсли ТипЗнч(Тип) = Тип("Тип") Тогда
		Типы = Новый Массив(1);
		Типы[0] = Тип;
		Возврат Новый ОписаниеТипов(Типы);
	Иначе
		Возврат Новый ОписаниеТипов(Тип);
	КонецЕсли;
	
КонецФункции

// Значение из строки.
// 
// Параметры:
//  ОписаниеТипов - см. ОписаниеТипов.Тип
//  Строка - Строка
// 
// Возвращаемое значение:
//  Произвольный
Функция ЗначениеИзСтроки(Знач ОписаниеТипов, Знач Строка) Экспорт
	
	ОписаниеТипов = ОписаниеТипов(ОписаниеТипов);

	Значение = ОписаниеТипов.ПривестиЗначение(Строка);
	Если ЗначениеЗаполнено(Строка) И НЕ ЗначениеЗаполнено(Значение) Тогда
		Если ОписаниеТипов.СодержитТип(Тип("УникальныйИдентификатор")) Тогда
			// BSLLS:MissingCodeTryCatchEx-off
			// @skip-check empty-except-statement - Не требует обработки - пытаемся получить УИД из строки
			Попытка
				Значение = Новый УникальныйИдентификатор(Строка); // BSLLS:MissingCodeTryCatchEx-off
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует ключ кэша поиска по правам пользователей.
// 
// Параметры:
//  ИмяПрава - Строка
//  ИмяМетаданного - Строка
// 
// Возвращаемое значение:
//  Строка
Функция КлючКэшаПоискаПоПравамПользователей(Знач ИмяПрава, ИмяМетаданного)
	
	ИмяПрава = ВРег(ИмяПрава);
	Если ПустаяСтрока(ИмяМетаданного) Тогда
		Возврат ИмяПрава;
	Иначе
		Возврат СтрШаблон("%1 | %2", ВРег(ИмяПрава), ВРег(ИмяМетаданного));
	КонецЕсли;
	
КонецФункции

#КонецОбласти
