
#Область СлужебныйПрограммныйИнтерфейс

// Словарь интерфейса.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - 
//  * Значение - Строка
Функция СловарьИнтерфейса() Экспорт
	
	Языки = ЯзыкиСловаря();
	
	ТекущийЯзык = ТекущийЯзык();
	Если ТекущийЯзык = Языки.Русский Тогда
		Результат = СловарьИнтерфейса_Русский();
	ИначеЕсли ТекущийЯзык = Языки.Английский Тогда
		Результат = СловарьИнтерфейса_Английский();
	Иначе
		ТекстОшибки = НСтр("ru='Не найден язык'; en='Language not found'");
		ТекстОшибки = СтрШаблон("%1 %2", ТекстОшибки, ТекущийЯзык);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  Фраза - Строка - Фраза на языке разработки
// 
// Возвращаемое значение:
//  Строка - Фраза на целевом языке интерфейса
Функция ФразаИнтерфейса(Фраза) Экспорт
	
	Словарь = РГП_ГлобальныйПоискКлиентСерверПовтИсп.СловарьИнтерфейса();
	
	Если Словарь.ЭтоЯзыкРазработки Тогда
		Возврат Фраза;
	КонецЕсли;
	
	ПолныйПеревод = Словарь.Перевод.Получить(Фраза);
	Если ПолныйПеревод = Неопределено Тогда
		Возврат Фраза;
	Иначе
		Возврат ПолныйПеревод;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СловарьИнтерфейса_Русский()
	
	Результат = НовыйСловарьИнтерфейса();
	Результат.ЭтоЯзыкРазработки = Истина;
	
	Возврат Результат;
	
КонецФункции

Функция СловарьИнтерфейса_Английский()
	
	Результат = НовыйСловарьИнтерфейса();
	Результат.Перевод = ПереводСловаряИнтефейса_Английский();
	
	Возврат Результат;
	
КонецФункции

Функция ПереводСловаряИнтефейса_Английский()
	
	Результат = НовыйПереводСловаряИнтерфейса();
	
	Результат.Вставить("Форма", "Form");
	Результат.Вставить("Формы", "Forms");
	Результат.Вставить("Версия", "Version");
	Результат.Вставить("Команда", "Command");
	Результат.Вставить("Открыть", "Open");
	Результат.Вставить("Закрыть", "Close");
	Результат.Вставить("Элемент", "Element");
	Результат.Вставить("Элементы", "Elements");
	Результат.Вставить("Изменить", "Edit");
	Результат.Вставить("Выводить", "Withdraw");
	Результат.Вставить("Выделить", "Highlight");
	Результат.Вставить("НаКлиенте", "AtClient");
	Результат.Вставить("НаСервере", "AtServer");
	Результат.Вставить("Метаданные", "Metadata");
	Результат.Вставить("Параметры", "Parametrs");
	Результат.Вставить("Настройки", "Settings");
	Результат.Вставить("Настройка", "Setting");
	Результат.Вставить("Избранное", "Favorites");
	Результат.Вставить("Выражение", "Expression");
	Результат.Вставить("Информация", "Information");
	Результат.Вставить("Не выводить", "Don't withdraw");
	Результат.Вставить("Активировать", "Activate");
	Результат.Вставить("Текущая версия", "Current version");
	Результат.Вставить("Поиск по формам", "Search by form");
	Результат.Вставить("Текущее значение", "Current value");
	Результат.Вставить("Введите выражение", "Enter the expression");
	Результат.Вставить("Сбросить настройки", "Reset settings");
	Результат.Вставить("Поиск по метаданным", "Metadata search");
	Результат.Вставить("Открыть репозиторий", "Open repository");
	Результат.Вставить("Вычислить выражение", "Execute expression");
	Результат.Вставить("Избранные команды поиска", "Favorite search commands");
	Результат.Вставить("Выводить варианты отчетов", "Display report options");
	Результат.Вставить("Не найдена команда поиска", "Search command not found");
	Результат.Вставить("О расширении глобального поиска", "About the extension of global search");
	Результат.Вставить("Передано некорректное имя общего модуля", "Incorrect common module name was passed");
	Результат.Вставить("Не найден обработчик стандартной команды", "Standard command handler not found");
	
	Результат.Вставить(
		"Поиск по именам, синонимам, представлениям и подсказкам", 
		"Search by name, synonyms, views and hints");
	Результат.Вставить(
		"Стоит ли выводить варианты отчетов и искать по ним?
		|	(может замедлять поиск)",
		"Should I display and search for report variants?
		|	(may slow down the search)"
		);
		
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
// * ЭтоЯзыкРазработки - Булево - Словарь для языка разработки (фразы не требуют перевода)
// * Перевод - см. НовыйПереводСловаряИнтерфейса
Функция НовыйСловарьИнтерфейса()
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоЯзыкРазработки", Ложь);
	Результат.Вставить("Перевод", НовыйПереводСловаряИнтерфейса());
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - Фраза на языке разработки
//  * Значение - Строка - Фраза на целевом языке интерфейса
Функция НовыйПереводСловаряИнтерфейса()
	
	Возврат Новый Соответствие;
	
КонецФункции


Функция ТекущийЯзык()
	
	Возврат НСтр("ru='ru';en='en'");
	
КонецФункции

Функция ЯзыкиСловаря()
	
	Результат = Новый Структура;
	Результат.Вставить("Английский", "en");
	Результат.Вставить("Русский", "ru");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти